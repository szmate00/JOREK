!> Calculates 3D integrals and boundary fluxes.
!! The NOMPIVERSION preprocessor flag is needed for mod_integrals3D_nompi, a version of this subroutine not requiring MPI.
#ifndef NOMPIVERSION
module mod_integrals3D
#endif

  use constants
  use mod_parameters
  use data_structure
  use gauss
  use basis_at_gaussian
  use tr_module
  use phys_module
  use mod_coupling_settings
  use coupling_variables
  use mod_interp
  use convert_character
  use mpi_mod
  use mod_expression
  use mod_plasma_functions
  use mod_poloidal_currents, only : integrated_normal_bnd_curr 
  use corr_neg
  use pellet_module
  use mod_chi
#if (defined WITH_Neutrals) && (!defined WITH_Impurities)
  use mod_neutral_source, only: total_neutral_source, total_n_particles, total_n_particles_inj, total_n_particles_inj_all
  use mod_source_shape, only: source_shape
#endif
#ifdef WITH_Impurities
  use mod_injection_source, only: total_imp_source, total_n_particles, total_n_particles_inj, total_n_particles_inj_all
  use mod_source_shape, only: source_shape
#endif
  use mod_impurity, only: radiation_function, radiation_function_linear
  use equil_info, only : get_psi_n, ES
  use mod_atomic_coeff_deuterium, only: rec_rate_to_kinetic, atomic_coeff_deuterium
  use mod_sources
  use mod_edge_elements, only : elm_coords

  implicit none
  
  private
  
  public :: int3d_new 
  
  contains



subroutine int3d_new(my_id, node_list, element_list, bnd_node_list, bnd_elm_list, expr_list, res, units, exclude_n0, aux_node_list)

!$ use omp_lib
 
implicit none

type (type_node_list),        intent(in)    :: node_list
type (type_element_list),     intent(in)    :: element_list   
type (type_bnd_node_list),    intent(in)    :: bnd_node_list
type (type_bnd_element_list), intent(in)    :: bnd_elm_list   
type (t_expr_list),           intent(in)    :: expr_list
real*8,                    intent(inout)    :: res(:)
integer,                      intent(in)    :: units
type(type_node_list),pointer, intent(inout), optional :: aux_node_list
logical, optional,            intent(in)    :: exclude_n0     !< Ommit n=0 component


! --- Local variables
type (type_element)      :: element, elm_k
type (type_node)         :: nodes(n_vertex_max), node_k
type (type_bnd_element)  :: bndelem
type (type_surface_list) :: surface_list
type (type_node) :: aux_nodes(n_vertex_max) 

real*8  :: psi_axis, psi_bnd
real*8  :: x_g(n_plane,n_gauss,n_gauss),        x_s(n_plane,n_gauss,n_gauss),        x_t(n_plane,n_gauss,n_gauss),        x_p(n_plane,n_gauss,n_gauss),         x_ss(n_plane,n_gauss,n_gauss),        x_tt(n_plane,n_gauss,n_gauss),        x_st(n_plane,n_gauss,n_gauss)
real*8  :: y_g(n_plane,n_gauss,n_gauss),        y_s(n_plane,n_gauss,n_gauss),        y_t(n_plane,n_gauss,n_gauss),        y_p(n_plane,n_gauss,n_gauss),         y_ss(n_plane,n_gauss,n_gauss),        y_tt(n_plane,n_gauss,n_gauss),        y_st(n_plane,n_gauss,n_gauss)
real*8, dimension(0:n_order-1,0:n_order-1,0:n_order-1) :: chi

real*8  :: eq_g(n_plane,0:n_var,n_gauss,n_gauss), eq_s(n_plane,0:n_var,n_gauss,n_gauss)
real*8  :: eq_t(n_plane,0:n_var,n_gauss,n_gauss), eq_p(n_plane,0:n_var,n_gauss,n_gauss)
real*8  :: eq_ss(n_plane,0:n_var,n_gauss,n_gauss), eq_tt(n_plane,0:n_var,n_gauss,n_gauss), eq_st(n_plane,0:n_var,n_gauss,n_gauss)
real*8  :: eq_sp(n_plane,0:n_var,n_gauss,n_gauss), eq_tp(n_plane,0:n_var,n_gauss,n_gauss)
real*8, dimension(n_plane,n_aux_var,n_gauss,n_gauss) :: eq_aux_g, eq_aux_s, eq_aux_t, eq_aux_p 
real*8  :: eq_spp(n_plane,0:n_var,n_gauss,n_gauss), eq_tpp(n_plane,0:n_var,n_gauss,n_gauss)
real*8  :: eq_s_3d(n_plane,0:n_var,n_gauss,n_gauss), eq_t_3d(n_plane,0:n_var,n_gauss,n_gauss)
real*8  :: wgauss_copy(n_gauss)
real*8  :: psi_axisym(n_gauss,n_gauss)
real*8  :: s_norm(n_gauss, n_gauss), stel_current_source(n_plane,n_gauss,n_gauss)

real*8  :: x_g_1D(n_plane,n_gauss),  x_s_1D(n_plane,n_gauss),   x_t_1D(n_plane,n_gauss)
real*8  :: y_g_1D(n_plane,n_gauss),  y_s_1D(n_plane,n_gauss),   y_t_1D(n_plane,n_gauss)
real*8  :: eq_g_1D(n_plane,0:n_var,n_gauss), eq_s_1D(n_plane,0:n_var,n_gauss)
real*8  :: delta_g_1D(n_plane,0:n_var,n_gauss)
real*8  :: eq_t_1D(n_plane,0:n_var,n_gauss), eq_p_1D(n_plane,0:n_var,n_gauss)
real*8  :: s_norm_1D(n_plane, n_gauss), stel_current_source_1D(n_plane,n_gauss)

real*8  :: current_source, particle_source, heat_source, heat_source_i, heat_source_e, rotation_source
real*8  :: xt, t_norm, rho_norm, t_norm2
real*8  :: eq_zne(n_gauss,n_gauss), eq_zTe(n_gauss,n_gauss)
real*8  :: dn_dpsi,dn_dz,dn_dpsi2,dn_dz2,dn_dpsi_dz,dn_dpsi3,dn_dpsi_dz2, dn_dpsi2_dz
real*8  :: dT_dpsi,dT_dz,dT_dpsi2,dT_dz2,dT_dpsi_dz,dT_dpsi3,dT_dpsi_dz2, dT_dpsi2_dz

integer :: i, j, k, in, ms, mt, mp, iv, inode, ife, n_elements, i_elm_axis, i_elm_xpoint(2), ifail
integer :: ierr, n_mpi, my_id, ife_delta, ife_min, ife_max, omp_nthreads, omp_tid
integer :: k_vertex, k_dof, k_node, k_dir, k_dir_perp, m_bndelem, dir_perp(2), mv1, m_elm
integer :: iexpr
real*8  :: R_c, Z_c, vec_inside(2), grad_t(2)
real*8  :: k_size, k_size_perp
real*8  :: G(4,n_degrees), sign_out, psi_n, ps0_sbnd, u0_sbnd
real*8  :: dt_back, dt_now, r_dt, r_dt2
real*8  :: I_halo, TPF, q02, q95, q99
real*8, allocatable :: qval(:), radav(:)

real*8  :: R_axis,Z_axis,s_axis,t_axis
real*8  :: current_tot, beta_p, beta_n, beta_t, aminor, current_MA, current_R_tot
real*8  :: xjac, xjac_R, xjac_Z, BigR, wst, P_int, P_e_int, P_i_int, mag_pres_int, C_intern, zj0, ps0, r0, T0, Te0, Ti0
real*8  :: Vol_in, Volume_in, Vol_ext, Volume_ext, Volume, Area, Bgeo, area1, surface_area
real*8  :: psi_as_coord
real*8  :: AR0, AR0_p, AR0_s, AR0_t, AR0_sp, AR0_tp, AR0_Rp, AZ0, AZ0_p, AZ0_s, AZ0_t, AZ0_sp, AZ0_tp, AZ0_Zp, A30
real*8  :: A30_p, A30_s, A30_t, A30_ss, A30_tt, A30_st, A30_R, A30_RR, A30_ZZ
real*8  :: BR_Z, BZ_R
real*8  :: r0_corr, T0_corr, Te0_corr, Ti0_corr, dTe0_corr_dT, T_or_Te, T_or_Te_corr, T_or_Te_0  
real*8  :: density_tot, density_in, density_out,  pressure, pressure_in, pressure_out, mag_pressure, mag_pressure_in, mag_pressure_out
real*8  :: pressure_e, pressure_e_in, pressure_e_out, pressure_i, pressure_i_in, pressure_i_out
real*8  :: current_in, current_out, D_int, D_ext, P_ext, mag_pres_ext, C_ext, delta_phi, phi, P_tot, mag_pres_tot, D_tot
real*8  :: beta_tot, beta_in, beta_out, vmec_beta_tot, vmec_beta_in, vmec_beta_out
real*8  :: VB_int, VB_ext, VB_tot
real*8  :: C_intern_3d, C_ext_3d, current_R_in, current_R_out, current_R, P_e_ext, P_i_ext, P_e_tot, P_i_tot
real*8  :: VP_int, VP_ext, VK_int, VK_ext, vpar0, Bv2, BB2, VP_tot, VK_tot
real*8  :: kin_par_in, kin_par_out, kin_par_tot, kin_perp_in, kin_perp_out, kin_perp_tot
real*8  :: local_mom_par_tot, local_mom_par_int, local_mom_par_ext
real*8  :: mom_par_tot, mom_par_int, mom_par_ext
real*8  :: VM_int, VM_ext, VM_tot, mag_in, mag_out, mag_tot, J2_int, J2_ext, J2_tot, ohm_in, ohm_tot, ohm_out
real*8  :: heli_tot, thm_wk, thm_wk_tot, mag_wk, mag_wk_tot, thermal_work_tot
real*8  :: vpar_disp_tot, vpar_disp, viscopar_dissip_tot, source_tot, heating_tot
real*8  :: vprp_disp_tot, vprp_disp, visco_dissip_tot, visco_T, visco_fact_old, visco_fact_new
real*8  :: fric_disp_tot, fric_disp, friction_dissip_tot
real*8  :: H_int, H_ext, S_int, S_ext, heating_in, heating_out, source_in, source_out
real*8  :: psi_xpoint(2),R_xpoint(2),Z_xpoint(2),s_xpoint(2),t_xpoint(2)
real*8  :: dTdx, dTdy, drhodx, drhody, dPdx, dPdy, dpsidx, dpsidy, dpsidp, dudx, dudy, dudp, drhondx, drhondy, drhoimpdx, drhoimpdy
real*8  :: dpsidx_3d, dpsidy_3d
real*8  :: dTedx, dTedy, dTidx, dTidy, dPedx, dPedy, dPidx, dPidy
real*8  :: w0, dwdx, dwdy, u0_xpp, u0_ypp
real*8  :: source_volume, source_pellet, eta_T, eta_T_ohm, Z_eff
real*8  :: local_pellet_particles, local_plasma_particles, local_pellet_volume
real*8  :: local_n_particles_inj, local_n_particles, rn0, rn0_corr, neut_particles_tot, rimp0, rimp0_corr
real*8  :: E_tot, E_in, E_out, Zkpar_T, D_prof, ZK_prof, sheath_heatflux
real*8  :: Px, Py, momentum_x, momentum_y
real*8  :: ZK_e_prof, ZK_i_prof, ZK_e_par_T, ZK_i_par_T
real*8  :: fact_mu0, fact_flux, fact_part
real*8  :: hel1, heli, helicity_tot, psi_off, curr, Ip, vn_p0, qn, pflow, kinflow, cond_par, cond_perp
real*8  :: kinpar_flux, qn_par, qn_perp, mag_work_tot, mag_src_tot, mag_source_tot
real*8  :: vpar_part_flux, vperp_part_flux, Dperp_part_flux, Dpar_part_flux, neut_part_flux
real*8  :: vpar_part_flow, vperp_part_flow, Dperp_part_flow, Dpar_part_flow, neut_part_flow
real*8  :: poynting_flux, poynting_tmp, dpsi_dt
real*8  :: s_or_t,sg,tg, st(2)
real*8  :: R,R_s,R_t,R_phi,R_st,R_ss,R_tt,R_sp,R_tp,R_pp
real*8  :: Z,Z_s,Z_t,Z_phi,Z_st,Z_ss,Z_tt,Z_sp,Z_tp,Z_pp
real*8  :: RH,RH_s,RH_t,RH_st,RH_ss,RH_tt
real*8  :: TT,TT_s,TT_t,TT_st,TT_ss,TT_tt 
real*8  :: UU,UU_s,UU_t,UU_st,UU_ss,UU_tt 
real*8  :: PS,PS_s,PS_t,PS_st,PS_ss,PS_tt 
real*8  :: vp,vp_s,vp_t,vp_st,vp_ss,vp_tt 
real*8  :: rn,rn_s,rn_t,rn_st,rn_ss,rn_tt 
real*8  :: rimp,rimp_s,rimp_t,rimp_st,rimp_ss,rimp_tt 
real*8  :: psi_s, psi_t, rho_s, rho_t, T_s, T_t, Ti, Ti_s, Ti_t, Te, Te_s, Te_t, p0_s, p0_t, u0_s, u0_t, ps0_s, ps0_t, p0_p, rhon_s, rhon_t, rhoimp_s, rhoimp_t
real*8  :: u0_p, u_s, u_t, u_p
real*8  :: u0_x, u0_y
real*8  :: viscopar_flux, viscopar_f, vpar_s, vpar_t, vpar_x, vpar_y, li3_tot, li3
real*8  :: varmin(n_var), varmax(n_var), V_min(n_var), V_max(n_var)

!> for use_ncs
real*8  :: aux_rho0, aux_E0, aux_mom_par0
real*8  :: aux_E0_Ti, aux_E0_Te
real*8  :: aux_P0, aux_P0_s,  aux_P0_t, aux_P0_p, aux_q0, aux_jx0, aux_jy0, aux_jz0, aux_jz0_pcs 
!Ionisation recombination for aux/use_ncs purposes. 
real*8  :: Nion, Nrec, Prec, Prb, Prb_cooling !Also needed for use_ncs
real*8  :: plasmaneutral, plasmaneutral_e, plasmaneutral_i, local_pn, local_pn_e, local_pn_i ! ncs/ics energy coupling terms (single T, Te, Ti)
real*8  :: local_Nion, local_Nrec, local_Prec, local_Prb, local_Prb_cooling  !Also needed for use_ncs
real*8  :: local_aux_mom_par_int ,local_aux_mom_par_ext, local_aux_mom_par_tot  ! coupled parallel momentum
real*8  :: aux_mom_par_int ,aux_mom_par_ext, aux_mom_par_tot  ! coupled parallel momentum
!> For model500 + use_ncs
real*8     :: ksi_ion_norm                                          ! Ionization energy shared with WITH_NEUTRALS
!   -Ionization
real*8     :: Sion_T_ncs, dSion_dT_ncs
!   -Recombination
real*8     :: Srec_T_ncs, dSrec_dT_ncs                                ! Recombination rate and its derivative wrt. temperature
!   -Radiation from injected gas/impurities
real*8     :: LradDcont_T_ncs, dLradDcont_dT_ncs                      ! Continuum (Brem.) radiation rate and its derivative wrt. T
real*8     :: LradDcont_corr_ncs, dLradDcont_dT_corr_ncs              ! LradDcont_T_ncs corrected for potential extra counting of dielectronic cascade energy loss
                                                                      ! (see mod_atomic_coeff_deuterium for more details)

!end for use_ncs
real*8  :: R_curr_cent, Z_curr_cent, Zcurr_tmp, R2curr_tmp, R2curr
real*8  :: heating_impl_in, heating_impl_out, H_impl_int, H_impl_ext,heating_impl_tot
real*8  :: Tie_min_neg, lnA
real*8  :: Bnorm, int_B_norm , L

real*8  :: cross_deriv(3), dA

#if (defined WITH_Neutrals)
real*8  :: source_neutral, source_neutral_drift
real*8  :: source_neutral_arr(n_inj_max), source_neutral_drift_arr(n_inj_max)
#endif
#ifdef WITH_Impurities
real*8  :: source_bg, source_imp, source_bg_drift, source_imp_drift
real*8  :: source_bg_arr(n_inj_max), source_imp_arr(n_inj_max), source_bg_drift_arr(n_inj_max), source_imp_drift_arr(n_inj_max) 
#endif
#if (defined WITH_Neutrals) || (defined WITH_Impurities)
real*8  :: local_radiation, local_radiation_cooling, local_radiation_bg, local_E_ion, total_radiation, total_radiation_cooling, total_radiation_bg, total_E_ion, local_P_ei, total_P_ei
real*8  :: local_P_ion, total_P_ion
real*8  :: local_radiation_phi(n_plane), local_radiation_cooling_phi(n_plane), total_radiation_phi(n_plane), total_radiation_cooling_phi(n_plane)
real*8  :: ne_SI, Te_eV, Te_corr_eV, Ti_eV

! SPI-related variables
integer    :: spi_i
integer    :: i_inj,  n_spi_tmp
real*8     :: spi_R_tmp
real*8     :: spi_Z_tmp
real*8     :: spi_phi_tmp
real*8     :: spi_psi_tmp
real*8     :: spi_grad_psi_tmp
real*8     :: ns_radius_tmp !< Radius of neutral gas cloud as a result of the ablation
real*8     :: source_tmp
real*8     :: ns_shape, ns_shape_drift ! variable for numerical integration of source volume
real*8     :: V_ns, V_ns_drift
real*8, allocatable :: local_source_volume(:), local_source_volume_drift(:)

#endif

! Additional diagnostic variables for impurity model
! See https://www.jorek.eu/wiki/doku.php?id=model500_501_555 for details
#ifdef WITH_Impurities
! Atomic physics coefficients:
!   -Mass ratio between main ions and impurites (m_i/m_imp)
real*8  :: m_i_over_m_imp, m_imp
!   -Mean impurity ionization state
real*8  :: Z_imp, dZ_imp_dT, eta_coef, ne_JOREK, dne_JOREK_dx, dne_JOREK_dy
real*8  :: Z_eff_imp
!   -Corrected plasma temperature and density for radiation calculation
real*8  :: Ti_corr_eV
!   -Temporary variable for charge state distribution
real*8, allocatable :: P_imp(:)
real*8     :: E_ion, Lrad, E_ion_bg
integer    :: ion_i, ion_k
#endif

!   -Coefficients related to Z_imp
real*8  :: alpha_i, alpha_e, dalpha_e_dT

!   -Ion-electron energy transfer
real*8  :: nu_e_imp, nu_e_bg, lambda_e_imp, lambda_e_bg, dTi_e, dTe_i
real*8  :: alpha_imp, dalpha_imp_dT, beta_imp, dbeta_imp_dT

! Additional variables related to the radiated power
#if (defined WITH_Neutrals)
! Atomic physics coefficients:
!   -Ionization
real*8     :: Sion_T, dSion_dT                                ! Ionization rate and its derivative wrt. temperature
!   -Recombination
real*8     :: Srec_T, dSrec_dT                                ! Recombination rate and its derivative wrt. temperature
!   -Radiation from injected gas/impurities
real*8     :: LradDrays_T, dLradDrays_dT                      ! Line (/rays) radiation rate and its derivative wrt. temperature
real*8     :: LradDcont_T, dLradDcont_dT                      ! Continuum (Brem.) radiation rate and its derivative wrt. T
real*8     :: LradDcont_corr, dLradDcont_dT_corr              ! LradDcont_T corrected for potential extra counting of dielectronic cascade energy loss
                                                              ! (see mod_atomic_coeff_deuterium for more details)
!   -Radiation from background impurities
real*8     :: Arad_bg, Brad_bg, Crad_bg                       ! Retain hard-coded fitting for argon
real*8     :: coef_prad_si                                    ! Prad,SI = coef_prad_si * Prad,jorek
#endif

#if (defined WITH_Neutrals) || (defined WITH_Impurities)
integer    :: i_imp, i_phi                                    ! Loop for more than one background impurity
real*8     :: frad_bg, Lrad_imp                               ! Retain hard-coded fitting for argon
#endif

! SAW energy functional (linear MHD)
real*8     :: SAW_tot, saw_energy_tot, saw_ene_dens, BB2_zero

#ifndef NOMPIVERSION
call MPI_COMM_SIZE(MPI_COMM_WORLD, n_mpi, ierr) ! number of MPI procs
n_mpi = max(n_mpi,1)
#else
n_mpi = 1
#endif

if (my_id .eq. 0) then
  write(*,*) '***************************************'
  write(*,*) '* Integrals  (3D)                     *'
  write(*,*) '***************************************'
  !write(*,*) ' n_plane : ',n_plane
  !write(*,*) ' n_mpi   : ',n_mpi
endif



density_tot  = 0.d0
pressure = 0.d0
pressure_i = 0.d0
pressure_e = 0.d0
mag_pres_tot = 0.d0
mag_pres_int = 0.d0
mag_pres_ext = 0.d0
D_int    = 0.d0
P_int    = 0.d0
P_e_int  = 0.d0
P_i_int  = 0.d0
C_intern = 0.d0
C_intern_3d = 0.d0
H_int    = 0.d0
H_impl_int = 0.d0
S_int    = 0.d0
VP_int   = 0.d0
local_mom_par_int = 0.d0 
VK_int   = 0.d0
VM_int   = 0.d0
VB_int   = 0.d0
J2_int   = 0.d0
D_ext    = 0.d0
P_ext    = 0.d0
P_e_ext  = 0.d0
P_i_ext  = 0.d0
C_ext    = 0.d0
C_ext_3d = 0.d0
H_ext    = 0.d0
H_impl_ext  = 0.d0
S_ext    = 0.d0
VP_ext   = 0.d0
local_mom_par_ext = 0.d0
VK_ext   = 0.d0
VM_ext   = 0.d0
VB_ext   = 0.d0
J2_ext   = 0.d0
Vol_in   = 0.d0
Vol_ext  = 0.d0
surface_area = 0.d0
area1    = 0.d0
P_tot    = 0.d0
P_e_tot  = 0.d0
P_i_tot  = 0.d0
D_tot    = 0.d0
wgauss_copy = wgauss
VP_tot   = 0.d0
local_mom_par_tot = 0.d0
VK_tot   = 0.d0
VM_tot   = 0.d0
VB_tot   = 0.d0
J2_tot   = 0.d0
hel1     = 0.d0
heli_tot = 0.d0
vn_p0    = 0.d0
qn_par   = 0.d0
qn_perp  = 0.d0
kinpar_flux  = 0.d0
poynting_flux= 0.d0
viscopar_flux= 0.d0
vpar_disp_tot= 0.d0
vprp_disp_tot= 0.d0
fric_disp_tot= 0.d0
psi_off      = 0.d0
thm_wk_tot   = 0.d0
mag_wk_tot   = 0.d0
mag_src_tot  = 0.d0
SAW_tot      = 0.d0
varmin   = +1.d99
varmax   = -1.d99
momentum_x = 0.d0
momentum_y = 0.d0
R2curr_tmp = 0.d0
Zcurr_tmp  = 0.d0

Dpar_part_flux   = 0.d0 
Dperp_part_flux  = 0.d0
vpar_part_flux   = 0.d0
vperp_part_flux  = 0.d0
neut_part_flux   = 0.d0

Bnorm = 0.d0
L = 0.d0 
int_B_norm = 0.d0

local_pellet_particles = 0.d0
local_plasma_particles = 0.d0
local_pellet_volume    = 0.d0

local_n_particles_inj = 0.d0
local_n_particles     = 0.d0

local_Nion = 0.d0
local_Nrec = 0.d0
local_pn   = 0.d0; local_pn_e   = 0.d0; local_pn_i   = 0.d0
local_Prec = 0.d0
local_Prb  = 0.d0        ! Radiation power (as would be measured by a bolometer, i.e. uses LradDCont_T)
local_Prb_cooling = 0.d0 ! Radiative cooling power (radiative energy lost from the plasma, i.e. uses LradDcont_corr)
local_aux_mom_par_int = 0.d0 
local_aux_mom_par_ext = 0.d0 
local_aux_mom_par_tot = 0.d0 

#if (defined WITH_Neutrals) || (defined WITH_Impurities)
local_radiation             = 0.d0 ! Radiation power (as would be measured by a bolometer, i.e. uses LradDCont_T)
local_radiation_cooling     = 0.d0 ! Radiative cooling power (radiative energy lost from the plasma, i.e. uses LradDcont_corr)
local_radiation_bg          = 0.d0
local_radiation_phi         = 0.d0 ! see local_radiation
local_radiation_cooling_phi = 0.d0 ! see local_radiation_cooling

local_E_ion           = 0.d0
local_P_ei            = 0.d0
local_P_ion           = 0.d0

if (using_spi) then
   if (allocated(local_source_volume)) then
      deallocate(local_source_volume)
   end if
   if (allocated(local_source_volume_drift)) then
      deallocate(local_source_volume_drift)
   end if

   allocate (local_source_volume(n_spi_tot))
   allocate (local_source_volume_drift(n_spi_tot))

   do spi_i=1, n_spi_tot
      local_source_volume(spi_i)            = 0.d0
      local_source_volume_drift(spi_i)      = 0.d0
   end do
end if
if (.not. allocated(local_source_volume)) allocate (local_source_volume(1)) ! Allocate a dummy array for omp
if (.not. allocated(local_source_volume_drift)) allocate (local_source_volume_drift(1)) 
#endif


delta_phi     = 2.d0 * PI / float(n_plane) / float(n_period)

psi_axis   = ES%psi_axis;        R_axis = ES%R_axis;        Z_axis = ES%Z_axis
psi_xpoint = ES%psi_xpoint;    R_xpoint = ES%R_xpoint;    Z_xpoint = ES%Z_xpoint 
psi_bnd    = ES%psi_bnd

ife_delta = ceiling(float(element_list%n_elements) / n_mpi)
ife_min   =      my_id     * ife_delta + 1
ife_max   = min((my_id +1) * ife_delta, element_list%n_elements)

#ifdef WITH_TiTe
Tie_min_neg = 0.5*T_min_neg
#endif

!$omp parallel default(none)                                                                                  &
!$omp   shared(element_list,node_list, aux_node_list, H, H_s, H_t, HZ, HZ_p, ife_min, ife_max, xpoint, xcase, &
!$omp          H_ss, H_tt, H_st, HZ_pp, HZ_coord, HZ_coord_p,                                                 &
!$omp          R_xpoint, Z_xpoint, my_id, use_pellet, delta_phi, R_axis, Z_axis, psi_axis, psi_bnd,           &
!$omp          D_tot, D_int, D_Ext, P_tot, P_int, P_ext, mag_pres_tot, mag_pres_int, mag_pres_ext,            &
!$omp          Vol_in, Vol_ext, surface_area, C_intern, C_ext, VP_ext, VP_int, VK_ext, VK_int, VK_tot,        &
!$omp          VM_ext, VM_int, VM_tot, VB_ext, VB_int, VB_tot, J2_tot, J2_ext, J2_int,                        &
!$omp          H_int, H_ext, S_int, S_ext,psi_xpoint,  F0, VP_tot,eta, T_0, Te_0, T_min,                      &
!$omp          ne_SI_min, Te_eV_min, rn0_min, P_e_tot, P_i_tot, P_e_int, P_i_int, P_e_ext, P_i_ext,           &
!$omp          C_intern_3d,C_ext_3d,pellet_amplitude,pellet_R,pellet_Z,pellet_psi,pellet_phi,                 &
!$omp          T_min_neg, Tie_min_neg, H_impl_int,H_impl_ext,implicit_heat_source,GAMMA,                      &
!$omp          pellet_radius, pellet_delta_psi, pellet_sig, pellet_length, pellet_ellipse, pellet_theta,      &
!$omp          central_density, pellet_particles,pellet_density, pellet_volume,                               &
!$omp          local_pellet_particles, local_plasma_particles, local_pellet_volume,                           &
!$omp          heli_tot,  keep_current_prof, psi_off, visco_par, visco_par_heating, thm_wk_tot,               &
!$omp          visco, visco_T_dependent, visco_old_setup, SAW_tot, mag_wk_tot,                                &
!$omp          vpar_disp_tot, vprp_disp_tot, fric_disp_tot, area1, mag_src_tot, momentum_x, momentum_y,       &
!$omp          eta_ohmic, central_mass, R2curr_tmp, Zcurr_tmp, ksi_ion,                                       &
!$omp          local_mom_par_int, local_mom_par_ext, local_mom_par_tot,                                       &
!$omp          use_ncs, use_ics, local_Nion, local_Nrec, local_Prec, local_Prb, local_Prb_cooling,            &
!$omp          local_aux_mom_par_int,local_aux_mom_par_ext,local_aux_mom_par_tot, n_aux_var,                  &
!$omp          rho_idx_kin, mom_par_idx_kin,                                                                  &
!$omp          local_pn_e, local_pn_i, local_pn,                                                              &
#ifdef WITH_TiTe
!$omp           E_Te_idx_kin, E_Ti_idx_kin,                                                                   &
#else
!$omp           E_idx_kin,                                                                                    &
#endif
#if (defined WITH_Neutrals) || (defined WITH_Impurities)
!$omp          spi_num_vol, local_source_volume, local_source_volume_drift, drift_distance,                   &
!$omp          using_spi, n_spi_tot, n_inj, n_spi,                                                            &
!$omp          pellets, ns_radius_ratio, ns_radius_min,                                                       &
!$omp          local_n_particles_inj, local_n_particles, ns_amplitude, ns_R, ns_Z,                            &
!$omp          ns_phi, ns_radius, ns_deltaphi, ns_delta_minor_rad, ns_tor_norm, spi_tor_rot, local_E_ion,     &
!$omp          t_now, A_Dmv, K_Dmv, V_Dmv, P_Dmv, t_ns, L_tube, JET_MGI,ASDEX_MGI, local_P_ion,               &
!$omp          local_radiation, local_radiation_phi, imp_cor, imp_adas, imp_type, local_P_ei,                 &
!$omp          n_adas, nimp_bg, local_radiation_cooling, local_radiation_cooling_phi, local_radiation_bg,     &
#endif
#if (defined WITH_Neutrals) && (!defined WITH_Impurities)
!$omp          use_imp_adas,                                                                                  &
#endif
#if (defined WITH_Impurities)
!$omp          index_main_imp,                                                                                &
#endif
!$omp          T_1, T_max_eta, T_max_eta_ohm, eta_T_dependent,                                                & 
!$omp          wgauss_copy, varmin, varmax)                                                                   &
!$omp   private(ife,iv,inode,element,i,j, k,in, mp, ms, mt,                                                   &
!$omp           x_g, y_g, x_s, y_s, x_t, y_t, x_p, y_p, xjac, xjac_R, xjac_Z, eq_g, eq_s, eq_t, eq_p,         &
!$omp           x_ss, x_tt, x_st, y_ss, y_tt, y_st, eq_ss, eq_tt, eq_st, eq_sp, eq_tp,                        &
!$omp           eq_spp, eq_tpp, psi_axisym,s_norm, stel_current_source,eq_s_3d, eq_t_3d, wst, BigR,           &
!$omp           r0, T0, Te0, zj0, ps0, dTdx, dTdy, drhodx, drhody, dpsidx, dpsidy, dpsidp, dudx, dudy,dudp,   &
!$omp           dpsidx_3d, dpsidy_3d, saw_ene_dens, BB2_zero,                                                 &
!$omp           w0, dwdx, dwdy, u0_xpp, u0_ypp, visco_T, visco_fact_old, visco_fact_new,                      &
!$omp           dpdx, dpdy, phi, Ti0, psi_as_coord, vprp_disp,                                                &
!$omp           source_pellet, source_volume, eq_zne, eq_zTe, vpar0, BB2, chi, Bv2,                           &
!$omp           heat_source, heat_source_i, heat_source_e, particle_source, current_source, rotation_source,  &
!$omp           dn_dpsi,dn_dz,dn_dpsi2,dn_dz2,dn_dpsi_dz,dn_dpsi3,dn_dpsi_dz2, dn_dpsi2_dz,                   &
!$omp           dT_dpsi,dT_dz,dT_dpsi2,dT_dz2,dT_dpsi_dz,dT_dpsi3,dT_dpsi_dz2, dT_dpsi2_dz,                   & 
!$omp           hel1, vpar_x, vpar_y, ps0_s, ps0_t, u0_s, u0_t, p0_s, p0_t, vpar_s, vpar_t,                   &
!$omp           u0_x, u0_y, Te0_corr, Ti0_corr, T_or_Te, T_or_Te_corr, T_or_Te_0,                             &
!$omp           thm_wk, mag_wk, eta_T, vpar_disp, fric_disp, p0_p, T0_corr, r0_corr, u0_p,                    &
!$omp           AR0, AR0_p, AR0_s, AR0_t, AR0_sp, AR0_tp, AR0_Rp, AZ0, AZ0_p, AZ0_s, AZ0_t, AZ0_sp, AZ0_tp,   &
!$omp           AZ0_Zp, A30, A30_p, A30_s, A30_t, A30_ss, A30_tt, A30_st, A30_R, A30_RR, A30_ZZ, BR_Z, BZ_R,  &
!$omp           Srec_T_ncs, dSrec_dT_ncs, ksi_ion_norm, LradDcont_T_ncs, dLradDcont_dT_ncs, Sion_T_ncs,       &
!$omp           LradDcont_corr_ncs, dLradDcont_dT_corr_ncs,                                                   &
!$omp           dSion_dT_ncs, eq_aux_g, eq_aux_s, eq_aux_t, eq_aux_p, aux_rho0, aux_mom_par0,                 &
!$omp           aux_P0, aux_P0_s, aux_P0_t, aux_P0_p, aux_q0, aux_jx0, aux_jy0, aux_jz0, aux_jz0_pcs,         &
!$omp           eta_T_ohm, rn0, rn0_corr, rimp0, rimp0_corr, Z_eff, lnA, alpha_e,                             &
!$omp           aux_E0_Ti, aux_E0_Te, aux_E0,                                                                 &
#if (defined WITH_Neutrals) || (defined WITH_Impurities)
!$omp           i_imp, frad_bg, Lrad_imp, Te_corr_eV, Te_eV, ne_SI, Ti_eV,                                    &
!$omp           spi_R_tmp, spi_Z_tmp, spi_phi_tmp, ns_radius_tmp,                                             &
!$omp           spi_psi_tmp, spi_grad_psi_tmp, spi_i, i_inj,                                                  &
!$omp           n_spi_tmp, source_tmp, ns_shape, ns_shape_drift,                                              &
#endif
#ifdef WITH_Impurities
!$omp           source_bg, source_imp, source_bg_drift, source_imp_drift,                                     &
!$omp           source_bg_arr, source_imp_arr, source_bg_drift_arr, source_imp_drift_arr,                     &
!$omp           m_i_over_m_imp, m_imp, Z_imp, dZ_imp_dT,                                                      &
!$omp           ne_JOREK, P_imp, Lrad, E_ion, E_ion_bg, ion_i,                                                &
!$omp           ion_k, Z_eff_imp, eta_coef, Ti_corr_eV,                                                       &
#endif
#if (defined WITH_Impurities) && (defined WITH_TiTe)
!$omp           alpha_i, nu_e_imp, nu_e_bg, lambda_e_imp, lambda_e_bg, dTi_e, dTe_i,                          &
!$omp           dalpha_e_dT,                                                                                  &
#endif
#if (defined WITH_Impurities) && (!defined WITH_TiTe) 
!$omp           alpha_imp, beta_imp, dalpha_imp_dT,                                                           &
#endif
#if (defined WITH_Neutrals)
!$omp           Sion_T, dSion_dT, Srec_T, dSrec_dT, source_neutral,                                           &
!$omp           source_neutral_drift, source_neutral_arr, source_neutral_drift_arr,                           &
!$omp           LradDrays_T, LradDcont_T, dLradDrays_dT, dLradDcont_dT,                                       &
!$omp           LradDcont_corr, dLradDcont_dT_corr,                                                           &
!$omp           Arad_bg, Brad_bg, Crad_bg,                                                                    &
!$omp           coef_prad_si,                                                                                 &
#endif
!$omp           omp_nthreads,omp_tid)                                                                         &
!$omp   firstprivate(nodes, aux_nodes) !< so that these nodes are unallocated at the start of the omp region and can be explicitly allocated/deallocated 



#ifdef OPENMP
omp_nthreads = omp_get_num_threads()
omp_tid      = omp_get_thread_num()
#else
omp_nthreads = 1
omp_tid      = 0
#endif

!$omp do reduction(+:local_pellet_particles, local_plasma_particles, local_pellet_volume,     &
#if (defined WITH_Neutrals) || (defined WITH_Impurities)
!$omp                local_n_particles_inj,  local_n_particles,                               &
!$omp                local_radiation, local_radiation_phi, local_E_ion, local_P_ei, local_P_ion, &
!$omp                local_source_volume, local_source_volume_drift, local_radiation_bg,      &
#endif
!$omp                D_int, D_ext, P_int, H_int, S_int, H_ext, S_ext, P_ext, C_intern, C_ext, &
!$omp                P_e_int, P_i_int, P_e_ext, P_i_ext, P_e_tot, P_i_tot,                    &
!$omp                mag_pres_tot, mag_pres_int, mag_pres_ext,                                &
!$omp                VP_int, VP_ext, VP_tot, VK_tot, VK_int, VK_ext, VM_ext,                  &
!$omp                VM_int, VM_tot, VB_ext, VB_int, VB_tot, Vol_in, Vol_ext,                 &
!$omp                surface_area, P_tot, D_tot,J2_tot, J2_int, J2_ext,                       &
!$omp                local_Nion, local_Nrec, local_pn, local_Prec, local_Prb ,                &
!$omp                local_aux_mom_par_int,local_aux_mom_par_ext,local_aux_mom_par_tot,       &
!$omp                local_mom_par_int, local_mom_par_ext, local_mom_par_tot,                        &
!$omp                heli_tot, mag_wk_tot, vpar_disp_tot, thm_wk_tot, area1, mag_src_tot,H_impl_int,H_impl_ext, &
!$omp                fric_disp_tot, R2curr_tmp, Zcurr_tmp, C_intern_3d, C_ext_3d, vprp_disp_tot)

do ife = ife_min, ife_max
  element = element_list%element(ife)

  do iv = 1, n_vertex_max
    inode     = element%vertex(iv)
    call make_deep_copy_node(node_list%node(inode), nodes(iv))
	
    if (present(aux_node_list)) call make_deep_copy_node(aux_node_list%node(inode), aux_nodes(iv)) 
  enddo

eq_aux_g = 0.d0; eq_aux_s = 0.d0; eq_aux_t = 0.d0; eq_aux_p = 0.d0;  
aux_rho0  = 0.d0; aux_E0    = 0.d0; aux_mom_par0 = 0.d0
aux_E0_Ti = 0.d0; aux_E0_Te = 0.d0
aux_P0    = 0.d0; aux_P0_s  = 0.d0; aux_P0_t  = 0.d0; aux_P0_p  = 0.d0
aux_q0    = 0.d0; aux_jx0   = 0.d0; aux_jy0   = 0.d0; aux_jz0   = 0.d0; aux_jz0_pcs = 0.d0
  
  
  
  x_g(:,:,:)    = 0.d0; x_s(:,:,:)    = 0.d0; x_t(:,:,:)    = 0.d0; x_p(:,:,:)    = 0.d0; x_ss(:,:,:)    = 0.d0; x_tt(:,:,:)    = 0.d0; x_st(:,:,:)    = 0.d0;
  y_g(:,:,:)    = 0.d0; y_s(:,:,:)    = 0.d0; y_t(:,:,:)    = 0.d0; y_p(:,:,:)    = 0.d0; y_ss(:,:,:)    = 0.d0; y_tt(:,:,:)    = 0.d0; y_st(:,:,:)    = 0.d0;
  psi_axisym(:,:) = 0.d0; s_norm(:,:) = 0.d0; stel_current_source(:,:,:) = 0.d0

  do i=1,n_vertex_max
    do j=1,n_degrees

      do ms=1, n_gauss
        do mt=1, n_gauss
          do mp=1,n_plane
            do in=1,n_coord_tor
              x_g(mp,ms,mt) = x_g(mp,ms,mt) + nodes(i)%x(in,j,1) * element%size(i,j) * H(i,j,ms,mt) * HZ_coord(in,mp)
              y_g(mp,ms,mt) = y_g(mp,ms,mt) + nodes(i)%x(in,j,2) * element%size(i,j) * H(i,j,ms,mt) * HZ_coord(in,mp)

              x_s(mp,ms,mt) = x_s(mp,ms,mt) + nodes(i)%x(in,j,1) * element%size(i,j) * H_s(i,j,ms,mt) * HZ_coord(in,mp)
              x_t(mp,ms,mt) = x_t(mp,ms,mt) + nodes(i)%x(in,j,1) * element%size(i,j) * H_t(i,j,ms,mt) * HZ_coord(in,mp)
              x_p(mp,ms,mt) = x_p(mp,ms,mt) + nodes(i)%x(in,j,1) * element%size(i,j) * H(i,j,ms,mt) * HZ_coord_p(in,mp)
              x_ss(mp,ms,mt) = x_ss(mp,ms,mt) + nodes(i)%x(in,j,1) * element%size(i,j) * H_ss(i,j,ms,mt) * HZ_coord(in,mp)
              x_tt(mp,ms,mt) = x_tt(mp,ms,mt) + nodes(i)%x(in,j,1) * element%size(i,j) * H_tt(i,j,ms,mt) * HZ_coord(in,mp)
              x_st(mp,ms,mt) = x_st(mp,ms,mt) + nodes(i)%x(in,j,1) * element%size(i,j) * H_st(i,j,ms,mt) * HZ_coord(in,mp)

              y_s(mp,ms,mt) = y_s(mp,ms,mt) + nodes(i)%x(in,j,2) * element%size(i,j) * H_s(i,j,ms,mt) * HZ_coord(in,mp)
              y_t(mp,ms,mt) = y_t(mp,ms,mt) + nodes(i)%x(in,j,2) * element%size(i,j) * H_t(i,j,ms,mt) * HZ_coord(in,mp)
              y_p(mp,ms,mt) = y_p(mp,ms,mt) + nodes(i)%x(in,j,2) * element%size(i,j) * H(i,j,ms,mt) * HZ_coord_p(in,mp)
              y_ss(mp,ms,mt) = y_ss(mp,ms,mt) + nodes(i)%x(in,j,2) * element%size(i,j) * H_ss(i,j,ms,mt) * HZ_coord(in,mp)
              y_tt(mp,ms,mt) = y_tt(mp,ms,mt) + nodes(i)%x(in,j,2) * element%size(i,j) * H_tt(i,j,ms,mt) * HZ_coord(in,mp)
              y_st(mp,ms,mt) = y_st(mp,ms,mt) + nodes(i)%x(in,j,2) * element%size(i,j) * H_st(i,j,ms,mt) * HZ_coord(in,mp)
            end do
          end do

#ifdef fullmhd
          ! --- Equilibrium psi (n=0 only)
          psi_axisym(ms,mt) = psi_axisym(ms,mt) + nodes(i)%values(1,j,var_A3) * element%size(i,j) * H(i,j,ms,mt)
#endif

#if STELLARATOR_MODEL
          s_norm(ms, mt) = s_norm(ms,mt) + nodes(i)%r_tor_eq(j)*element%size(i,j)*H(i,j,ms,mt)
          do mp=1,n_plane
            do in=1,n_tor
              stel_current_source(mp,ms,mt) = stel_current_source(mp,ms,mt) + nodes(i)%j_source(in,j)*element%size(i,j)*H(i,j,ms,mt)*HZ(in,mp)
            end do
          end do
#endif

        enddo
      enddo
    enddo
  enddo

  eq_g(:,:,:,:) = 0.d0; eq_s(:,:,:,:) = 0.d0; eq_t(:,:,:,:) = 0.d0; eq_p(:,:,:,:) = 0.d0; eq_ss(:,:,:,:) = 0.d0; eq_tt(:,:,:,:) = 0.d0; eq_st(:,:,:,:) = 0.d0; 
  eq_sp(:,:,:,:) = 0.d0; eq_tp(:,:,:,:) = 0.d0; eq_spp(:,:,:,:) = 0.d0; eq_tpp(:,:,:,:) = 0.d0; eq_s_3d(:,:,:,:) = 0.d0; eq_t_3d(:,:,:,:) = 0.d0;
  do i=1,n_vertex_max
    do j=1,n_degrees

      do mp=1,n_plane
        do ms=1, n_gauss
          do mt=1, n_gauss

            do k=1,n_var
              do in=1,n_tor
                eq_g(mp,k,ms,mt) = eq_g(mp,k,ms,mt) + nodes(i)%values(in,j,k) * element%size(i,j) * H(i,j,ms,mt)  * HZ(in,mp)
                eq_s(mp,k,ms,mt) = eq_s(mp,k,ms,mt) + nodes(i)%values(in,j,k) * element%size(i,j) * H_s(i,j,ms,mt)* HZ(in,mp)
                eq_t(mp,k,ms,mt) = eq_t(mp,k,ms,mt) + nodes(i)%values(in,j,k) * element%size(i,j) * H_t(i,j,ms,mt)* HZ(in,mp)
                eq_p(mp,k,ms,mt) = eq_p(mp,k,ms,mt) + nodes(i)%values(in,j,k) * element%size(i,j) * H(i,j,ms,mt)  * HZ_p(in,mp)
                eq_sp(mp,k,ms,mt) = eq_sp(mp,k,ms,mt) + nodes(i)%values(in,j,k) * element%size(i,j) * H_s(i,j,ms,mt)* HZ_p(in,mp)
                eq_tp(mp,k,ms,mt) = eq_tp(mp,k,ms,mt) + nodes(i)%values(in,j,k) * element%size(i,j) * H_t(i,j,ms,mt)* HZ_p(in,mp)
                eq_ss(mp,k,ms,mt) = eq_ss(mp,k,ms,mt) + nodes(i)%values(in,j,k) * element%size(i,j) * H_ss(i,j,ms,mt)* HZ(in,mp)
                eq_tt(mp,k,ms,mt) = eq_tt(mp,k,ms,mt) + nodes(i)%values(in,j,k) * element%size(i,j) * H_tt(i,j,ms,mt)* HZ(in,mp)
                eq_st(mp,k,ms,mt) = eq_st(mp,k,ms,mt) + nodes(i)%values(in,j,k) * element%size(i,j) * H_st(i,j,ms,mt)* HZ(in,mp)
                eq_spp(mp,k,ms,mt) = eq_spp(mp,k,ms,mt) + nodes(i)%values(in,j,k) * element%size(i,j) * H_s(i,j,ms,mt)*HZ_pp(in,mp)
                eq_tpp(mp,k,ms,mt) = eq_tpp(mp,k,ms,mt) + nodes(i)%values(in,j,k) * element%size(i,j) * H_t(i,j,ms,mt)*HZ_pp(in,mp)
                
                if ( in == 1 ) cycle ! Record only the non-axisymmetric components
                eq_s_3d(mp,k,ms,mt) = eq_s_3d(mp,k,ms,mt) + nodes(i)%values(in,j,k) * element%size(i,j) * H_s(i,j,ms,mt)* HZ(in,mp)
                eq_t_3d(mp,k,ms,mt) = eq_t_3d(mp,k,ms,mt) + nodes(i)%values(in,j,k) * element%size(i,j) * H_t(i,j,ms,mt)* HZ(in,mp)
             
              enddo !n_tor
            enddo !n_var

            if (present(aux_node_list)) then
              do k=1,n_aux_var
                do in=1,n_tor
                  eq_aux_g(mp,k,ms,mt) =  eq_aux_g(mp,k,ms,mt) + aux_nodes(i)%values(in,j,k) * element%size(i,j) * H(i,j,ms,mt)   * HZ(in,mp)
                  eq_aux_s(mp,k,ms,mt) =  eq_aux_s(mp,k,ms,mt) + aux_nodes(i)%values(in,j,k) * element%size(i,j) * H_s(i,j,ms,mt) * HZ(in,mp)
                  eq_aux_t(mp,k,ms,mt) =  eq_aux_t(mp,k,ms,mt) + aux_nodes(i)%values(in,j,k) * element%size(i,j) * H_t(i,j,ms,mt) * HZ(in,mp)
                  eq_aux_p(mp,k,ms,mt) =  eq_aux_p(mp,k,ms,mt) + aux_nodes(i)%values(in,j,k) * element%size(i,j) * H(i,j,ms,mt)   * HZ_p(in,mp)
                enddo
              enddo
            endif ! present(aux_node_list)

	        enddo !mt n_gauss
        enddo !ms n_gauss
      enddo !mp nplane
    enddo !j n_degrees
  enddo !i n_vertex_max
   
  ! --- Determine smallest and largest values of the variables in the whole domain (on Gauss points and toroidal integration surfaces)
  !$omp critical
  do k = 1, n_var
    varmin(k) = min( varmin(k), minval(eq_g(:,k,:,:)) )
    varmax(k) = max( varmax(k), maxval(eq_g(:,k,:,:)) )
  end do
  !$omp end critical
  
  mp = 1   ! eq_zne and eq_zTe are only used in tokamak models
  do ms=1, n_gauss
    do mt=1, n_gauss
      call density(xpoint, xcase, y_g(mp,ms,mt), Z_xpoint, eq_g(1,var_psi,ms,mt),psi_axis,psi_bnd,eq_zne(ms,mt), &
                   dn_dpsi,dn_dz,dn_dpsi2,dn_dz2,dn_dpsi_dz,dn_dpsi3,dn_dpsi_dz2, dn_dpsi2_dz)

#ifdef WITH_TiTe
      call temperature_e(xpoint, xcase, y_g(mp,ms,mt), Z_xpoint, eq_g(1,1,ms,mt),psi_axis,psi_bnd,eq_zTe(ms,mt), &
                       dT_dpsi,dT_dz,dT_dpsi2,dT_dz2,dT_dpsi_dz,dT_dpsi3,dT_dpsi_dz2, dT_dpsi2_dz)
#else
      call temperature(xpoint, xcase, y_g(mp,ms,mt), Z_xpoint, eq_g(1,1,ms,mt),psi_axis,psi_bnd,eq_zTe(ms,mt),   &
                       dT_dpsi,dT_dz,dT_dpsi2,dT_dz2,dT_dpsi_dz,dT_dpsi3,dT_dpsi_dz2, dT_dpsi2_dz)
      eq_zTe(ms,mt) = eq_zTe(ms,mt) / 2.d0	! electron temperature
#endif
    enddo
  enddo

  !--------------------------------------------------- sum over the Gaussian integration points
  do mp=1,n_plane

    phi       = 2.d0*PI*float(mp-1)/float(n_plane) / float(n_period)

    do ms=1, n_gauss
      do mt=1, n_gauss

        wst  = wgauss_copy(ms)*wgauss_copy(mt)

        xjac = x_s(mp,ms,mt)*y_t(mp,ms,mt) - x_t(mp,ms,mt)*y_s(mp,ms,mt)
        xjac_R = (x_ss(mp,ms,mt)*y_t(mp,ms,mt)**2 - 2*x_st(mp,ms,mt)*y_s(mp,ms,mt)*y_t(mp,ms,mt) + x_tt(mp,ms,mt)*y_s(mp,ms,mt)**2 &
               + x_s(mp,ms,mt)*(y_st(mp,ms,mt)*y_t(mp,ms,mt) - y_tt(mp,ms,mt)*y_s(mp,ms,mt)) + x_t(mp,ms,mt)*(y_st(mp,ms,mt)*y_s(mp,ms,mt) &
               - y_ss(mp,ms,mt)*y_t(mp,ms,mt)))/xjac
        xjac_Z = (y_ss(mp,ms,mt)*x_t(mp,ms,mt)**2 - 2*y_st(mp,ms,mt)*x_s(mp,ms,mt)*x_t(mp,ms,mt) + y_tt(mp,ms,mt)*x_s(mp,ms,mt)**2 &
               + y_s(mp,ms,mt)*(x_st(mp,ms,mt)*x_t(mp,ms,mt) - x_tt(mp,ms,mt)*x_s(mp,ms,mt)) + y_t(mp,ms,mt)*(x_st(mp,ms,mt)*x_s(mp,ms,mt) &
               - x_ss(mp,ms,mt)*x_t(mp,ms,mt)))/xjac
        BigR = x_g(mp,ms,mt)

#if STELLARATOR_MODEL
        chi = get_chi(x_g(mp,ms,mt),y_g(mp,ms,mt),phi,node_list,element_list,ife,xgauss(ms),xgauss(mt))
        Bv2 = chi(1,0,0)**2 + chi(0,1,0)**2 + chi(0,0,1)**2/BigR**2
#endif

        r0     = eq_g(mp,var_rho,ms,mt)
        r0_corr = corr_neg_dens(r0)
#ifdef WITH_TiTe
        Ti0    = eq_g(mp,var_Ti,ms,mt)
        Te0    = eq_g(mp,var_Te,ms,mt)
        Te0_corr = corr_neg_temp(Te0*2.d0) / 2.d0
        Ti0_corr = corr_neg_temp(Ti0*2.d0) / 2.d0
        T_or_Te       = Te0
        T_or_Te_corr  = Te0_corr
        T_or_Te_0     = Te_0
#else
        T0       = eq_g(mp,var_T,ms,mt)
        Ti0      = eq_g(mp,var_T,ms,mt) /2.d0
        Te0      = eq_g(mp,var_T,ms,mt) /2.d0
        T0_corr  = corr_neg_temp(T0)
        Te0_corr = T0_corr / 2.d0
        Ti0_corr = T0_corr / 2.d0
        T_or_Te       = T0
        T_or_Te_corr  = T0_corr
        T_or_Te_0     = T_0
#endif
        zj0    = eq_g(mp,var_zj,ms,mt)
        ps0    = eq_g(mp,var_psi,ms,mt)
        ps0_s  = eq_s(mp,var_psi,ms,mt) 
        ps0_t  = eq_t(mp,var_psi,ms,mt)
        u0_s   = eq_s(mp,var_u,ms,mt) 
        u0_t   = eq_t(mp,var_u,ms,mt)
        u0_p   = eq_p(mp,var_u,ms,mt)
        u0_x   = (   y_t(mp,ms,mt) * eq_s(mp,var_u,ms,mt) - y_s(mp,ms,mt) * eq_t(mp,var_u,ms,mt) ) / xjac
        u0_y   = ( - x_t(mp,ms,mt) * eq_s(mp,var_u,ms,mt) + x_s(mp,ms,mt) * eq_t(mp,var_u,ms,mt) ) / xjac

        vpar0   = eq_g(mp,var_Vpar,ms,mt)
        vpar_s  = eq_s(mp,var_Vpar,ms,mt)
        vpar_t  = eq_t(mp,var_Vpar,ms,mt)

        if (use_ncs .or. use_ics) then
                if (use_ncs) aux_rho0     = eq_aux_g(mp,rho_idx_kin,ms,mt)
#ifdef WITH_TiTe
                aux_E0_Te = eq_aux_g(mp,E_Te_idx_kin,ms,mt)
                aux_E0_Ti = eq_aux_g(mp,E_Ti_idx_kin,ms,mt)
#else
                aux_E0       = eq_aux_g(mp,E_idx_kin,ms,mt)
#endif
                aux_mom_par0 = eq_aux_g(mp,mom_par_idx_kin,ms,mt)
        end if

#if (defined WITH_Neutrals)
        rn0    = eq_g(mp,var_rhon,ms,mt)
        rn0_corr = corr_neg_dens(rn0, (/ 0.d-5, 1.d-5 /)) ! Correction for negative rn0
#else
        rn0      = 0.d0
        rn0_corr = 0.d0 
#endif
#if (defined WITH_Impurities)
        rimp0  = eq_g(mp,var_rhoimp,ms,mt)
        rimp0_corr = corr_neg_dens(rimp0, (/ 0.d-5, 1.d-5 /)) ! Correction for negative rimp0
#else
        rimp0  = 0.d0
        rimp0_corr = 0.d0 
#endif
      
#ifdef fullmhd
        AR0      = eq_g(mp,var_AR,ms,mt)
        AR0_p    = eq_p(mp,var_AR,ms,mt)
        AR0_s    = eq_s(mp,var_AR,ms,mt)
        AR0_t    = eq_t(mp,var_AR,ms,mt)
        AR0_sp   = eq_sp(mp,var_AR,ms,mt)
        AR0_tp   = eq_tp(mp,var_AR,ms,mt)
        AR0_Rp   = (   y_t(mp,ms,mt) * AR0_sp  - y_s(mp,ms,mt) * AR0_tp ) / xjac
        AZ0      = eq_g(mp,var_AZ,ms,mt)
        AZ0_p    = eq_p(mp,var_AZ,ms,mt)
        AZ0_s    = eq_s(mp,var_AZ,ms,mt)
        AZ0_t    = eq_t(mp,var_AZ,ms,mt)
        AZ0_sp   = eq_sp(mp,var_AZ,ms,mt)
        AZ0_tp   = eq_tp(mp,var_AZ,ms,mt)
        AZ0_Zp   = ( - x_t(mp,ms,mt) * AZ0_sp  + x_s(mp,ms,mt) * AZ0_tp ) / xjac
        A30      = eq_g(mp,var_A3,ms,mt)
        A30_p    = eq_p(mp,var_A3,ms,mt)
        A30_s    = eq_s(mp,var_A3,ms,mt)
        A30_t    = eq_t(mp,var_A3,ms,mt)
        A30_ss   = eq_ss(mp,var_A3,ms,mt)
        A30_tt   = eq_tt(mp,var_A3,ms,mt)
        A30_st   = eq_st(mp,var_A3,ms,mt)
        A30_R = (   y_t(mp,ms,mt) * A30_s  - y_s(mp,ms,mt) * A30_t ) / xjac
        A30_RR   = (A30_ss * y_t(mp,ms,mt)**2 - 2.d0*A30_st * y_s(mp,ms,mt)*y_t(mp,ms,mt) + A30_tt * y_s(mp,ms,mt)**2  &
                    + A30_s * (y_st(mp,ms,mt)*y_t(mp,ms,mt) - y_tt(mp,ms,mt)*y_s(mp,ms,mt) )                           &
                    + A30_t * (y_st(mp,ms,mt)*y_s(mp,ms,mt) - y_ss(mp,ms,mt)*y_t(mp,ms,mt) ) )       / xjac**2         &
                    - xjac_R * (A30_s * y_t(mp,ms,mt) - A30_t * y_s(mp,ms,mt))     / xjac**2
        A30_ZZ   = (A30_ss * x_t(mp,ms,mt)**2 - 2.d0*A30_st * x_s(mp,ms,mt)*x_t(mp,ms,mt) + A30_tt * x_s(mp,ms,mt)**2  &
                    + A30_s * (x_st(mp,ms,mt)*x_t(mp,ms,mt) - x_tt(mp,ms,mt)*x_s(mp,ms,mt) )                           &
                    + A30_t * (x_st(mp,ms,mt)*x_s(mp,ms,mt) - x_ss(mp,ms,mt)*x_t(mp,ms,mt) ) )       / xjac**2         &
                    - xjac_Z * (- A30_s * x_t(mp,ms,mt) + A30_t * x_s(mp,ms,mt) )  / xjac**2
        BR_Z = ( A30_ZZ - AZ0_Zp )/ BigR
        BZ_R = -1/BigR**2 * ( AR0_p - A30_R ) + ( AR0_Rp - A30_RR )/ BigR
        zj0 = - (BZ_R - BR_Z) * BigR
        psi_as_coord = psi_axisym(ms,mt)
#elif STELLARATOR_MODEL
        psi_as_coord = s_norm(ms, mt)
#else
        psi_as_coord = ps0
#endif
        ! This is currently broken for two temperature models !
        ! Some of these do not seem to be doing anything so I'm commenting them off !
        !dTdx   = (   y_t(mp,ms,mt) * eq_s(mp,var_T,ms,mt) - y_s(mp,ms,mt) * eq_t(mp,var_T,ms,mt) ) / xjac
        !dTdy   = ( - x_t(mp,ms,mt) * eq_s(mp,var_T,ms,mt) + x_s(mp,ms,mt) * eq_t(mp,var_T,ms,mt) ) / xjac
        !drhodx = (   y_t(mp,ms,mt) * eq_s(mp,var_rho,ms,mt) - y_s(mp,ms,mt) * eq_t(mp,var_rho,ms,mt) ) / xjac
        !drhody = ( - x_t(mp,ms,mt) * eq_s(mp,var_rho,ms,mt) + x_s(mp,ms,mt) * eq_t(mp,var_rho,ms,mt) ) / xjac

        dpsidx = (   y_t(mp,ms,mt) * eq_s(mp,var_psi,ms,mt) - y_s(mp,ms,mt) * eq_t(mp,var_psi,ms,mt) ) / xjac
        dpsidy = ( - x_t(mp,ms,mt) * eq_s(mp,var_psi,ms,mt) + x_s(mp,ms,mt) * eq_t(mp,var_psi,ms,mt) ) / xjac
        dpsidp = eq_p(mp,var_psi,ms,mt) - dpsidx*x_p(mp,ms,mt) - dpsidy*y_p(mp,ms,mt)

        dudx = (   y_t(mp,ms,mt) * eq_s(mp,var_u,ms,mt) - y_s(mp,ms,mt) * eq_t(mp,var_u,ms,mt) ) / xjac
        dudy = ( - x_t(mp,ms,mt) * eq_s(mp,var_u,ms,mt) + x_s(mp,ms,mt) * eq_t(mp,var_u,ms,mt) ) / xjac
        dudp = eq_p(mp,var_u,ms,mt) - dudx*x_p(mp,ms,mt) - dudy*y_p(mp,ms,mt)

        w0   = eq_g(mp,var_w,ms,mt)
        dwdx = (   y_t(mp,ms,mt) * eq_s(mp,var_w,ms,mt) - y_s(mp,ms,mt) * eq_t(mp,var_w,ms,mt) ) / xjac
        dwdy = ( - x_t(mp,ms,mt) * eq_s(mp,var_w,ms,mt) + x_s(mp,ms,mt) * eq_t(mp,var_w,ms,mt) ) / xjac

        u0_xpp = (   y_t(mp,ms,mt) * eq_spp(mp,var_u,ms,mt) - y_s(mp,ms,mt) * eq_tpp(mp,var_u,ms,mt) ) / xjac
        u0_ypp = ( - x_t(mp,ms,mt) * eq_spp(mp,var_u,ms,mt) + x_s(mp,ms,mt) * eq_tpp(mp,var_u,ms,mt) ) / xjac

        vpar_x = (   y_t(mp,ms,mt) * vpar_s - y_s(mp,ms,mt) * vpar_t ) / xjac
        vpar_y = ( - x_t(mp,ms,mt) * vpar_s + x_s(mp,ms,mt) * vpar_t ) / xjac

        BB2 = (F0*F0 + dpsidx*dpsidx + dpsidy*dpsidy) / BigR**2

        dpsidx_3d = (   y_t(mp,ms,mt) * eq_s_3d(mp,var_psi,ms,mt) - y_s(mp,ms,mt) * eq_t_3d(mp,var_psi,ms,mt) ) / xjac
        dpsidy_3d = ( - x_t(mp,ms,mt) * eq_s_3d(mp,var_psi,ms,mt) + x_s(mp,ms,mt) * eq_t_3d(mp,var_psi,ms,mt) ) / xjac

        !dPdx = r0 * dTdx + T0 * drhodx
        !dPdy = r0 * dTdy + T0 * drhody

#ifdef WITH_TiTe
        call sources(xpoint, xcase, y_g(mp,ms,mt), Z_xpoint, psi_as_coord, psi_axis, psi_bnd, &
                     particle_source,heat_source_i,heat_source_e)
        heat_source = heat_source_i + heat_source_e
#else
        call sources(xpoint, xcase, y_g(mp,ms,mt), Z_xpoint, psi_as_coord, psi_axis, psi_bnd, &
                     particle_source,heat_source)
#endif
        if (keep_current_prof) then
#if STELLARATOR_MODEL
          current_source = stel_current_source(mp,ms,mt)
#else
          call current(xpoint, xcase, x_g(mp,ms,mt),y_g(mp,ms,mt), Z_xpoint, psi_as_coord,&
                       psi_axis,psi_bnd,current_source)
#endif
        else
          current_source = 0.d0
        endif

!-------------------------------------------
! --- USE NCS, PARTICLE NEUTRAL AND IMPURITY COUPLE SCHEME
! ------------------------------------------
        if(use_ncs .or. use_ics) then 
          ksi_ion_norm = central_density * 1.d20 * ksi_ion
          call rec_rate_to_kinetic(r0, 0.5d0*T0, Sion_T_ncs, dSion_dT_ncs, Srec_T_ncs, dSrec_dT_ncs, &
                                   LradDcont_T_ncs, dLradDcont_dT_ncs, LradDcont_corr_ncs, dLradDcont_dT_corr_ncs)
        
          !> coupled densities
          !>ionization
          local_Nion = local_Nion + aux_rho0 * BigR * xjac * delta_phi * wst
          !> Recombination
          local_Nrec = local_Nrec + (Srec_T_ncs * r0_corr * r0_corr)*BigR *xjac * delta_phi *wst ! rho_rec
          
          !> coupled energies
          !>aux_E0 (plasma neutral interaction)
#ifdef WITH_TiTe
          local_pn_e = local_pn_e + aux_E0_Te *BigR * xjac* delta_phi * wst
          local_pn_i = local_pn_i + aux_E0_Ti *BigR * xjac* delta_phi * wst
#else
          local_pn = local_pn + aux_E0 *BigR * xjac* delta_phi * wst !& !aux_E0
                      !+ (gamma-1.d0)* 0.5d0 *aux_rho0 *vpar0**2 * BB2 * BigR*xjac* delta_phi *wst &
                      !- (gamma-1.d0)* aux_mom_par0 * vpar0 * BigR *xjac* delta_phi *wst
#endif
          !>Lost to recombination (no Brehmstralung)
          local_Prec = local_Prec + r0_corr*r0_corr*(T0_corr*Srec_T_ncs)*BigR *xjac* delta_phi *wst
          ! Radiation power of recombination and bremsstrahlung combined
          local_Prb         = local_Prb         + r0_corr*r0_corr *LradDcont_T_ncs    *BigR *xjac* delta_phi *wst
          ! Radiative cooling power of recombination and bremsstrahlung combined
          local_Prb_cooling = local_Prb_cooling + r0_corr*r0_corr *LradDcont_corr_ncs *BigR *xjac* delta_phi *wst
          !> aux_vpar = dot_product(SI momentum source,B). so we  divide by |B| to obtain the integral of the SI momentum
          local_aux_mom_par_tot=local_aux_mom_par_tot+ aux_mom_par0 /sqrt(BB2) * xjac * BigR * wst * delta_phi !< * sqrt(BB2)

        endif ! use_ncs  
 

!-------------------------------------------
! --- Radiation and ionization power
! ------------------------------------------
#if ( (defined WITH_Neutrals) && (! defined WITH_Impurities) )
        ! --- Get ionization, recombination and radiation coefficients for Deuterium 
#ifdef WITH_TiTe
        call atomic_coeff_deuterium  (   Te0, Sion_T, dSion_dT, Srec_T, dSrec_dT, LradDcont_T, dLradDcont_dT, &
                                         LradDcont_corr, dLradDcont_dT_corr, LradDrays_T, dLradDrays_dT, r0, rn0, .true. ) 
#else
        call atomic_coeff_deuterium(0.5d0*T0, Sion_T, dSion_dT, Srec_T, dSrec_dT, LradDcont_T, dLradDcont_dT, &
                                         LradDcont_corr, dLradDcont_dT_corr, LradDrays_T, dLradDrays_dT, r0, rn0, .true. ) 
#endif
     
        ! Get coefficient:  Prad,SI = coef_prad_si * Prad,jorek
        coef_prad_si = 1./((GAMMA-1)*MU_ZERO*(MU_ZERO*central_mass*ATOMIC_MASS_UNIT*central_density*1.d20)**0.5) 
      
        ksi_ion_norm = central_density * 1.d20 * ksi_ion   ! Normalisation of the ionization energy cost for Deuterium
      
        ! --- Radiation from background impurity
        ne_SI = r0_corr * 1.d20 * central_density !electron density (SI)

        if (with_TiTe) then 
          Te_corr_eV =       Te0_corr/(EL_CHG*MU_ZERO*central_density*1.d20)  ! Te in eV
          Te_eV =       Te0/(EL_CHG*MU_ZERO*central_density*1.d20)  ! Te in eV, uncorrected
        else
          Te_corr_eV = 0.5d0* T0_corr/(EL_CHG*MU_ZERO*central_density*1.d20)  ! Te in eV
          Te_eV = 0.5d0* T0/(EL_CHG*MU_ZERO*central_density*1.d20)  ! Te in eV, uncorrected
        endif

        if (use_imp_adas) then  ! use open adas by default  
          ! Use radiation coefficients from ADAS
          frad_bg = 0. 
          do i_imp = 1, n_adas     
            if (ne_SI > ne_SI_min .and. Te_eV > Te_eV_min .and. nimp_bg(i_imp) > 0) then
              Lrad_imp = 0.0
              call radiation_function_linear(imp_adas(i_imp),imp_cor(i_imp),log10(ne_SI),    & 
                                           log10(Te_corr_eV*EL_CHG/K_BOLTZ),.false.,Lrad_imp)           
            else     
              Lrad_imp = 0.
            end if
            frad_bg = frad_bg + nimp_bg(i_imp) * Lrad_imp
          end do

          local_radiation_phi(mp)         = local_radiation_phi(mp) + ( (r0_corr * rn0_corr  * LradDrays_T    &
                                            + r0_corr ** 2 * LradDcont_T) * coef_prad_si                      & 
                                            + ne_SI * frad_bg) * bigR * xjac * wst * delta_phi 
          local_radiation_cooling_phi(mp) = local_radiation_cooling_phi(mp) + ( (r0_corr * rn0_corr  * LradDrays_T    &
                                            + r0_corr ** 2 * LradDcont_corr) * coef_prad_si                           & 
                                            + ne_SI * frad_bg) * bigR * xjac * wst * delta_phi                               
                                     
          local_radiation                 = local_radiation + ( (r0_corr * rn0_corr  * LradDrays_T            &
                                            + r0_corr ** 2 * LradDcont_T) * coef_prad_si                      & 
                                            + ne_SI * frad_bg) * bigR * xjac * wst * delta_phi 
          local_radiation_cooling         = local_radiation_cooling + ( (r0_corr * rn0_corr  * LradDrays_T    &
                                            + r0_corr ** 2 * LradDcont_corr) * coef_prad_si                   & 
                                            + ne_SI * frad_bg) * bigR * xjac * wst * delta_phi 

          local_P_ion             = local_P_ion + ksi_ion_norm * r0_corr * rn0_corr * Sion_T * coef_prad_si &
                                   * bigR * xjac * wst * delta_phi
        else
          if ( trim(imp_type(1)) == 'Ar') then ! Hard-coded fitting exists for argon
            Arad_bg = 2.4d-31 
            Brad_bg = 20.
            Crad_bg = 0.8
            frad_bg = (2./3.)*(1./(central_mass*ATOMIC_MASS_UNIT))*((MU_ZERO*central_mass*ATOMIC_MASS_UNIT*central_density*1.d20)**(1.5d0))                &
                            *nimp_bg(1)*Arad_bg*exp(-((log(Te_corr_eV)-log(Brad_bg))**2.)/Crad_bg**2.)
                    
            local_radiation_phi(mp)         = local_radiation_phi(mp) + (r0_corr * rn0_corr  * LradDrays_T         &
                                              + r0_corr ** 2 * LradDcont_T + r0_corr * frad_bg) * coef_prad_si     & 
                                              * bigR * xjac * wst * delta_phi  
            local_radiation_cooling_phi(mp) = local_radiation_cooling_phi(mp) + (r0_corr * rn0_corr  * LradDrays_T &
                                              + r0_corr ** 2 * LradDcont_corr + r0_corr * frad_bg) * coef_prad_si  & 
                                              * bigR * xjac * wst * delta_phi  

            local_radiation                 = local_radiation + (r0_corr * rn0_corr  * LradDrays_T &
                                              + r0_corr ** 2 * LradDcont_T + r0_corr * frad_bg) * coef_prad_si & 
                                              * bigR * xjac * wst * delta_phi 
            local_radiation_cooling         = local_radiation_cooling + (r0_corr * rn0_corr  * LradDrays_T &
                                              + r0_corr ** 2 * LradDcont_corr + r0_corr * frad_bg) * coef_prad_si & 
                                              * bigR * xjac * wst * delta_phi 

            local_P_ion             = local_P_ion + ksi_ion_norm * r0_corr * rn0_corr * Sion_T * coef_prad_si &
                                       * bigR * xjac * wst * delta_phi
          else
            write(*,*) "WARNING: hard-coded fitting doesn't exist for  ", trim(imp_type(1)), ", use open adas instead!"
            stop
          end if
        end if

#endif

#ifdef WITH_Impurities
        !-------------------------------------------
        ! Atomic physics parameters for Impurities
        !-------------------------------------------

        select case ( trim(imp_type(index_main_imp)) )
          case('D2')
            m_i_over_m_imp = central_mass/2.  ! Deuterium mass = 2 u
            m_imp          = 2.
          case('Ar')
            m_i_over_m_imp = central_mass/40. ! Argon mass = 40 u
            m_imp          = 40.
          case('Ne')
            m_i_over_m_imp = central_mass/20. ! Neon mass = 20 u
            m_imp          = 20.
          case default
            write(*,*) '!! Gas type "', trim(imp_type(index_main_imp)), '" unknown (in mod_injection_source.f90) !!'
            write(*,*) '=> We assume the gas is D2.'
            m_i_over_m_imp = central_mass/2.
            m_imp          = 2.
        end select

        ! Temperatures in eV and corrected values:
        if (with_TiTe) then 
          Ti_eV      = Ti0          /(EL_CHG*MU_ZERO*central_density*1.d20)
          Te_eV      = Te0          /(EL_CHG*MU_ZERO*central_density*1.d20)  
          Ti_corr_eV = Ti0_corr     /(EL_CHG*MU_ZERO*central_density*1.d20)
          Te_corr_eV = Te0_corr     /(EL_CHG*MU_ZERO*central_density*1.d20)  
        else
          Ti_eV      = 0.5d0*T0     /(EL_CHG*MU_ZERO*central_density*1.d20)  
          Te_eV      = 0.5d0*T0     /(EL_CHG*MU_ZERO*central_density*1.d20)  
          Ti_corr_eV = 0.5d0*T0_corr/(EL_CHG*MU_ZERO*central_density*1.d20)  
          Te_corr_eV = 0.5d0*T0_corr/(EL_CHG*MU_ZERO*central_density*1.d20)  
        endif

        if (allocated(P_imp)) deallocate(P_imp)
        allocate(P_imp(0:imp_adas(index_main_imp)%n_Z))
        call imp_cor(index_main_imp)%interp_linear(density=20.,temperature=log10(Te_corr_eV*EL_CHG/K_BOLTZ),&
                                      p_out=P_imp,z_avg=Z_imp, z_avg_Te=dZ_imp_dT)

        if (allocated(imp_adas(index_main_imp)%ionisation_energy)) then
   
          ! Calculate the ionization potential energy and its derivative wrt. temperature
          E_ion     = 0.
          E_ion_bg  = 13.6
          do ion_i=1, imp_adas(index_main_imp)%n_Z
            do ion_k=1, ion_i
              E_ion     = E_ion + P_imp(ion_i)*imp_adas(index_main_imp)%ionisation_energy(ion_k)
            end do
          end do
          ! Convert from eV to SI unit
          E_ion     = E_ion * EL_CHG
          E_ion_bg  = E_ion_bg * EL_CHG
        else
          E_ion     = 0.
          E_ion_bg  = 0.
        end if

#ifdef WITH_TiTe
        alpha_i       = m_i_over_m_imp - 1.
        alpha_e       = m_i_over_m_imp*Z_imp - 1.
        dalpha_e_dT   = m_i_over_m_imp*dZ_imp_dT
  
        ne_SI        = (r0_corr + alpha_e * rimp0_corr) * 1.d20 * central_density ! electron density (SI)
        ne_JOREK     = r0_corr + alpha_e * rimp0_corr ! Electron density in JOREK unit
        ne_JOREK     = corr_neg_dens(ne_JOREK,(/1.d-1,1.d-1/),1.d-3) ! Correction for negative electron density
                                                            ! Too small rho_1 will cause a problem
#else /* WITH_TiTe */
        alpha_imp    = 0.5*m_i_over_m_imp*(Z_imp+1.) - 1.
        dalpha_imp_dT= 0.5*m_i_over_m_imp*dZ_imp_dT
        beta_imp     = m_i_over_m_imp*Z_imp - 1.
        alpha_e      = beta_imp
        ne_SI        = (r0_corr + beta_imp * rimp0_corr) * 1.d20 * central_density !electron density (SI)
        ne_JOREK     = r0_corr + beta_imp * rimp0_corr ! Electron density in JOREK unit
        ne_JOREK     = corr_neg_dens(ne_JOREK,(/1.d-1,1.d-1/),1.d-3) ! Correction for negative electron density
                                                               ! Too small rho_1 will cause a problem
#endif /* WITH_TiTe */

        ! This is to avoid too small ne_SI in some logarithm caoculation
        ne_SI = max(1.d16,ne_SI)

        ! Calculate the effective charge of all species
        Z_eff        = 0.
        Z_eff_imp    = 0.
   
        ! First get the value of Z_eff
        Z_eff        = r0_corr - rimp0_corr
        do ion_i=1, imp_adas(index_main_imp)%n_Z
          Z_eff      = Z_eff + m_i_over_m_imp * rimp0_corr * P_imp(ion_i) * real(ion_i,8)**2
          Z_eff_imp  = Z_eff_imp + P_imp(ion_i) * real(ion_i,8)**2 ! The summation of normalized nZ**2 for impurity
        end do
        Z_eff        = Z_eff / ne_JOREK
  
        if (Z_eff < 1) Z_eff = 1.
        if (Z_eff > imp_adas(1)%n_Z)  Z_eff = imp_adas(1)%n_Z
   
        if (ne_SI > ne_SI_min .and. Te_eV > Te_eV_min .and. rimp0 > rn0_min) then
          Lrad = 0.0
          !call radiation_function(imp_adas(index_main_imp),imp_cor(index_main_imp),log10(ne_SI),log10(Te_corr_eV*EL_CHG/K_BOLTZ),Lrad)
          call radiation_function_linear(imp_adas(index_main_imp),imp_cor(index_main_imp),log10(ne_SI),log10(Te_corr_eV*EL_CHG/K_BOLTZ),.false.,Lrad)
        else
          Lrad = 0.
        end if

        Lrad = Lrad * m_i_over_m_imp
        E_ion = E_ion * m_i_over_m_imp

        frad_bg = 0. 
        do i_imp = 1, n_adas     
          if (i_imp == index_main_imp) cycle
          if (ne_SI > ne_SI_min .and. Te_eV > Te_eV_min .and. nimp_bg(i_imp) > 0) then
            Lrad_imp = 0.0
            call radiation_function_linear(imp_adas(i_imp),imp_cor(i_imp),log10(ne_SI),    & 
                                         log10(Te_eV*EL_CHG/K_BOLTZ),.false.,Lrad_imp)           
          else     
            Lrad_imp = 0.
          end if
          frad_bg = frad_bg + nimp_bg(i_imp) * Lrad_imp
        end do

        local_radiation_phi(mp) = local_radiation_phi(mp) &
                                  + ne_SI * (rimp0_corr * central_density * 1.d20 * Lrad + frad_bg)&
                                  * bigR * xjac * wst * delta_phi        
        local_radiation = local_radiation &
                          + ne_SI * (rimp0_corr * central_density * 1.d20 * Lrad + frad_bg)&
                          * bigR * xjac * wst * delta_phi 
        local_radiation_bg = local_radiation_bg &
                             + ne_SI * frad_bg * bigR * xjac * wst * delta_phi 
        local_E_ion     = local_E_ion + rimp0 * central_density * 1.d20 * E_ion             &
                          * bigR * xjac * wst * delta_phi
        local_E_ion     = local_E_ion + (r0 - rimp0) * central_density * 1.d20 * E_ion_bg   &
                          * bigR * xjac * wst * delta_phi
#ifdef WITH_TiTe
       !--------------------------------------------------------
       ! --- Ion-electron energy transfer
       !--------------------------------------------------------
        if (Te_corr_eV < 10.*Z_imp**2) then
          lambda_e_imp = 23. - log((ne_SI*1.d-6)**0.5*Z_imp*Te_corr_eV**(-1.5))
        else
          lambda_e_imp = 24. - log((ne_SI*1.d-6)**0.5*Te_corr_eV**(-1.0))
        endif
        if (Te_corr_eV < 10.) then
          lambda_e_bg  = 23. - log((ne_SI*1.d-6)**0.5*Te_corr_eV**(-1.5)) ! Assuming bg_charge is 1! 
        else
          lambda_e_bg  = 24. - log((ne_SI*1.d-6)**0.5*Te_corr_eV**(-1.0))
        endif
        nu_e_imp     = 1.8d-19*(1.d6*MASS_ELECTRON*ATOMIC_MASS_UNIT*m_imp) ** 0.5&
                       * Z_eff_imp * (1.d14*central_density*rimp0_corr*m_i_over_m_imp) * lambda_e_imp &
                       / (1.d3*(MASS_ELECTRON*Ti0_corr+Te0_corr*ATOMIC_MASS_UNIT*m_imp)&
                       / (EL_CHG * MU_ZERO * central_density * 1.d20)) ** 1.5
        nu_e_bg      = 1.8d-19*(1.d6*MASS_ELECTRON*ATOMIC_MASS_UNIT*central_mass) ** 0.5&
                       * (1.d14*central_density*(r0_corr-rimp0_corr)) * lambda_e_bg &
                       / (1.d3*(MASS_ELECTRON*Ti0_corr+Te0_corr*ATOMIC_MASS_UNIT*central_mass)&
                       / (EL_CHG * MU_ZERO * central_density * 1.d20)) ** 1.5 ! Assuming bg_charge is 1!
    
        if (nu_e_imp < 0.) nu_e_imp = 0.
        if (nu_e_bg < 0.)  nu_e_bg  = 0.
    
        dTe_i    = (nu_e_imp + nu_e_bg) * (Ti_corr_eV - Te_corr_eV) * EL_CHG ! To SI unit
        local_P_ei = local_P_ei + ne_SI * dTe_i * bigR * xjac * wst * delta_phi 
#endif /* WITH_TiTe */
#endif /* WITH_Impurities */

#if (! defined WITH_Impurities)
        Z_eff      = 1.d0
        rimp0      = 0.d0
        rimp0_corr = 0.d0
        alpha_e    = 0.d0
#endif 
        call coulomb_log_ei(T_or_Te, T_or_Te_corr, r0, r0_corr, rimp0, rimp0_corr, alpha_e, lnA)
        call resistivity(eta,       T_or_Te, T_or_Te_corr, T_max_eta,     T_or_Te_0, Z_eff, lnA, eta_T    )           
        call resistivity(eta_ohmic, T_or_Te, T_or_Te_corr, T_max_eta_ohm, T_or_Te_0, Z_eff, lnA, eta_T_ohm)           

        ! --- Switch to use old viscosity model
        if (visco_old_setup) then
          visco_fact_old = 1.d0 / BigR**2.d0    ! Recover R^2 dependence
          visco_fact_new = 0.d0                 ! Switch off new viscosity terms
        else
          visco_fact_old = 1.d0 
          visco_fact_new = 1.d0 
        endif
        call viscosity(visco, T_or_Te, T_or_Te_corr,T_or_Te_0, visco_T)

#ifdef WITH_Impurities
        D_tot  = D_tot  + (r0-rimp0) * xjac * BigR * wst * delta_phi 
#ifdef WITH_TiTe
        P_e_tot = P_e_tot + (r0+alpha_e*rimp0) * Te0 * xjac * BigR * wst * delta_phi
        P_i_tot = P_i_tot + (r0+alpha_i*rimp0) * Ti0 * xjac * BigR * wst * delta_phi
        P_tot   = P_e_tot + P_i_tot

        p0_s   = (r0+alpha_i*rimp0)*eq_s(mp,var_Ti,ms,mt) &
                 + Ti0 * (eq_s(mp,var_rho,ms,mt)+alpha_i*eq_s(mp,var_rhoimp,ms,mt))&
                 + (r0+alpha_e*rimp0+dalpha_e_dT*rimp0*Te0)*eq_s(mp,var_Te,ms,mt)&
                 + Te0 * (eq_s(mp,var_rho,ms,mt)+alpha_e*eq_s(mp,var_rhoimp,ms,mt))
        p0_t   = (r0+alpha_i*rimp0)*eq_t(mp,var_Ti,ms,mt) &
                 + Ti0 * (eq_t(mp,var_rho,ms,mt)+alpha_i*eq_t(mp,var_rhoimp,ms,mt))&
                 + (r0+alpha_e*rimp0+dalpha_e_dT*rimp0*Te0)*eq_t(mp,var_Te,ms,mt)&
                 + Te0 * (eq_t(mp,var_rho,ms,mt)+alpha_e*eq_t(mp,var_rhoimp,ms,mt))
        p0_p   = (r0+alpha_i*rimp0)*eq_p(mp,var_Ti,ms,mt) &
                 + Ti0 * (eq_p(mp,var_rho,ms,mt)+alpha_i*eq_p(mp,var_rhoimp,ms,mt))&
                 + (r0+alpha_e*rimp0+dalpha_e_dT*rimp0*Te0)*eq_p(mp,var_Te,ms,mt)&
                 + Te0 * (eq_p(mp,var_rho,ms,mt)+alpha_e*eq_p(mp,var_rhoimp,ms,mt))
#else /* WITH_TiTe */
        P_tot  = P_tot  + (r0+alpha_imp*rimp0) * T0 * xjac * BigR * wst * delta_phi
        P_e_tot = P_tot / 2.
        P_i_tot = P_e_tot

        p0_s   = (r0+alpha_imp*rimp0+dalpha_imp_dT*rimp0*T0)*eq_s(mp,var_T,ms,mt) &
                 + T0 * (eq_s(mp,var_rho,ms,mt)+alpha_imp*eq_s(mp,var_rhoimp,ms,mt))
        p0_t   = (r0+alpha_imp*rimp0+dalpha_imp_dT*rimp0*T0)*eq_t(mp,var_T,ms,mt) &
                 + T0 * (eq_t(mp,var_rho,ms,mt)+alpha_imp*eq_t(mp,var_rhoimp,ms,mt))
        p0_p   = (r0+alpha_imp*rimp0+dalpha_imp_dT*rimp0*T0)*eq_p(mp,var_T,ms,mt) &
                 + T0 * (eq_p(mp,var_rho,ms,mt)+alpha_imp*eq_p(mp,var_rhoimp,ms,mt))
#endif /* WITH_TiTe */
#else /* WITH_Impurities */
        D_tot  = D_tot  + r0       * xjac * BigR * wst * delta_phi
#ifdef WITH_TiTe
        P_e_tot = P_e_tot + r0 * Te0 * xjac * BigR * wst * delta_phi
        P_i_tot = P_i_tot + r0 * Ti0 * xjac * BigR * wst * delta_phi
        P_tot   = P_e_tot + P_i_tot

        p0_s   = r0*eq_s(mp,var_Te,ms,mt) + Te0 * eq_s(mp,var_rho,ms,mt) &
                 +r0*eq_s(mp,var_Ti,ms,mt) + Ti0 * eq_s(mp,var_rho,ms,mt)
        p0_t   = r0*eq_t(mp,var_Te,ms,mt) + Te0 * eq_t(mp,var_rho,ms,mt) &
                 +r0*eq_t(mp,var_Ti,ms,mt) + Ti0 * eq_t(mp,var_rho,ms,mt)
        p0_p   = r0*eq_p(mp,var_Te,ms,mt) + Te0 * eq_p(mp,var_rho,ms,mt) &
                 +r0*eq_p(mp,var_Ti,ms,mt) + Ti0 * eq_p(mp,var_rho,ms,mt)
#else /* WITH_TiTe */

        P_tot  = P_tot  + r0 * T0 * xjac * BigR * wst * delta_phi
        P_e_tot = P_tot / 2.
        P_i_tot = P_e_tot

        p0_s   = r0*eq_s(mp,var_T,ms,mt) + T0 * eq_s(mp,var_rho,ms,mt) 
        p0_t   = r0*eq_t(mp,var_T,ms,mt) + T0 * eq_t(mp,var_rho,ms,mt) 
        p0_p   = r0*eq_p(mp,var_T,ms,mt) + T0 * eq_p(mp,var_rho,ms,mt) 
#endif /* WITH_TiTe */
#endif /* WITH_Impurities */

        thm_wk     = vpar0 * (p0_s*ps0_t - p0_t*ps0_s) + vpar0 * F0/BigR*p0_p*xjac 
        hel1       = F0* ( (ps0 - psi_off) - y_g(mp,ms,mt)*dpsidy) / (BigR**2.d0)
        mag_wk     = - (ps0_s*u0_t - ps0_t*u0_s) / xjac * zj0 / BigR  &
                     + F0 * zj0 * u0_p / (BigR**2.d0)

        vpar_disp  = visco_par * (vpar_x**2.d0+vpar_y**2.d0 ) 
        vprp_disp  = -visco_T * ( BigR**2.d0 * ( dwdx*dudx + dwdy*dudy ) * visco_fact_old  &
                                 + 2.d0 * BigR * w0 * dudx               * visco_fact_new  &
                                 + u0_xpp * dudx + u0_ypp * dudy   )     * visco_fact_new

        VP_tot = VP_tot + r0 * vpar0**2 * BB2 * xjac * BigR * wst * delta_phi
        local_mom_par_tot = local_mom_par_tot + r0 * vpar0 * sqrt(BB2) * xjac * BigR * wst * delta_phi
#if STELLARATOR_MODEL
        VK_tot = VK_tot + r0*((dudx**2 + dudy**2 + dudp**2/BigR**2)/Bv2 &
               - (chi(1,0,0)*dudx + chi(0,1,0)*dudy + chi(0,0,1)*dudp/BigR**2)**2/Bv2**2)*xjac*BigR*wst*delta_phi
        VM_tot = VM_tot + (Bv2*(dpsidx**2 + dpsidy**2 + dpsidp**2/BigR**2) &
               - (chi(1,0,0)*dpsidx + chi(0,1,0)*dpsidy + chi(0,0,1)*dpsidp/BigR**2)**2)*xjac*BigR*wst*delta_phi/F0**2
        VB_tot  = VB_tot + 2.0*r0*T0 / (Bv2*(1.d0 + (dpsidx**2 + dpsidy**2 + dpsidp**2/BigR**2)/F0**2) &
                                   - ((chi(1,0,0)*dpsidx + chi(0,1,0)*dpsidy + chi(0,0,1)*dpsidp/BigR**2)**2)/F0**2) *xjac*BigR*wst*delta_phi
        mag_pres_tot = mag_pres_tot + (Bv2*(1.d0 + (dpsidx**2 + dpsidy**2 + dpsidp**2/BigR**2)/F0**2) &
                                    - ((chi(1,0,0)*dpsidx + chi(0,1,0)*dpsidy + chi(0,0,1)*dpsidp/BigR**2)**2)/F0**2)/2 *xjac*BigR*wst*delta_phi
#else
        VK_tot = VK_tot + r0 * (dudx**2 + dudy**2) * BigR**2 * xjac * BigR * wst * delta_phi
        VM_tot = VM_tot + (dpsidx**2+dpsidy**2)/BigR**2 * xjac * BigR * wst * delta_phi
        VB_tot = VB_tot + 2.0*r0*T0 / BB2 * xjac * BigR * wst * delta_phi
        mag_pres_tot = mag_pres_tot + BB2/2 * xjac * BigR * wst * delta_phi
#endif
        J2_tot = J2_tot + eta_T_ohm *(ZJ0/BigR)**2.d0 * xjac * BigR * wst * delta_phi
        
        ! Momentum in the Cartesian x- and y-directions
        momentum_x = momentum_x + r0*(-BigR**2*dudy*cos(phi) - F0*vpar0*sin(phi) + vpar0*dpsidy*cos(phi))*xjac*wst*delta_phi
        momentum_y = momentum_y + r0*( BigR**2*dudy*sin(phi) - F0*vpar0*cos(phi) - vpar0*dpsidy*sin(phi))*xjac*wst*delta_phi

        mag_src_tot   = mag_src_tot + eta_T*ZJ0*current_source/(BigR**2) * xjac * BigR * wst * delta_phi

        heli_tot      = heli_tot   + hel1         * BigR * xjac * wst * delta_phi
        mag_wk_tot    = mag_wk_tot + mag_wk       * BigR * xjac * wst * delta_phi
        thm_wk_tot    = thm_wk_tot + thm_wk                     * wst * delta_phi
        vpar_disp_tot = vpar_disp_tot + vpar_disp * BigR * xjac * wst * delta_phi
        vprp_disp_tot = vprp_disp_tot + vprp_disp * BigR * xjac * wst * delta_phi

        R2curr_tmp    = R2curr_tmp - x_g(mp,ms,mt)**2.0 * zj0 /BigR * xjac * wst * delta_phi    
        Zcurr_tmp     = Zcurr_tmp  - y_g(mp,ms,mt)      * zj0 /BigR * xjac * wst * delta_phi    

        if (use_pellet) then
          call pellet_source2(pellet_amplitude,pellet_R,pellet_Z,pellet_psi,pellet_phi, &
                              pellet_radius, pellet_delta_psi, pellet_sig, pellet_length, pellet_ellipse, pellet_theta, &
                              x_g(mp,ms,mt),y_g(mp,ms,mt), psi_as_coord, phi, r0_corr, T0_corr/2.d0, central_density, &
                              pellet_particles, pellet_density, pellet_volume, source_pellet, source_volume)

          local_pellet_particles = local_pellet_particles + source_pellet * bigR * xjac * wst * delta_phi
          local_plasma_particles = local_plasma_particles + r0            * bigR * xjac * wst * delta_phi
          local_pellet_volume    = local_pellet_volume    + source_volume * bigR * xjac * wst * delta_phi
        endif

#if (defined WITH_Neutrals) || (defined WITH_Impurities)
        if (using_spi) then

           if (JET_MGI .or. ASDEX_MGI) then
              write(*,*) "WARNING: Using SPI, disabling MGI settings"
              JET_MGI = .false.
              ASDEX_MGI = .false.
           end if

           do spi_i=1, n_spi_tot

              n_spi_tmp = 0
              do i_inj = 1, n_inj
                 n_spi_tmp = n_spi_tmp + n_spi(i_inj)
                 if (spi_i <= n_spi_tmp)  exit !< Determine the injection location index of the fragment
              end do

              if (t_now >= t_ns(i_inj)) then

                 source_tmp = 0.d0
                 ns_shape = 0.d0
                 ns_shape_drift = 0.d0

                 spi_R_tmp   = pellets(spi_i)%spi_R
                 spi_Z_tmp   = pellets(spi_i)%spi_Z
                 spi_phi_tmp = pellets(spi_i)%spi_phi

                 spi_psi_tmp = pellets(spi_i)%spi_psi
                 spi_grad_psi_tmp = pellets(spi_i)%spi_grad_psi
                 
                 ns_radius_tmp   = pellets(spi_i)%spi_radius * ns_radius_ratio

                 if (ns_radius_tmp < ns_radius_min) then
                    ns_radius_tmp = ns_radius_min
                 end if

                 ! Compute the source shape
                 ns_shape = source_shape(x_g(mp,ms,mt),y_g(mp,ms,mt),phi, &
                      spi_R_tmp,spi_Z_tmp,spi_phi_tmp,              &
                      ns_radius_tmp,ns_deltaphi,                    &
                      ps0,spi_psi_tmp,spi_grad_psi_tmp,ns_delta_minor_rad)
                 
                 ! To detect NaNs
                 if (ns_shape /= ns_shape) then
                   write(*,*) 'ERROR in mod_integrals3D: ns_shape = ', ns_shape
                   stop
                 end if

                 local_source_volume(spi_i) = local_source_volume(spi_i) &
                      + ns_shape * bigR * xjac * wst * delta_phi

                 if (drift_distance(i_inj) /= 0) then ! Get the volume at the post-drift location (for normalization)

                   if (pellets(spi_i)%plasmoid_in_domain == 1) then ! if the drifted location is within the domain

                     ns_shape_drift = source_shape(x_g(mp,ms,mt),y_g(mp,ms,mt),phi,     &
                          spi_R_tmp+drift_distance(i_inj),spi_Z_tmp,spi_phi_tmp,  &
                          ns_radius_tmp,ns_deltaphi,                        &
                          ps0,pellets(spi_i)%spi_psi_drift,                 &
                          pellets(spi_i)%spi_grad_psi_drift,                &
                          ns_delta_minor_rad)

                     ! To detect NaNs
                     if (ns_shape_drift /= ns_shape_drift) then
                       write(*,*) 'ERROR in mod_integrals3D: ns_shape_drift = ', ns_shape_drift
                       stop
                     end if

                     local_source_volume_drift(spi_i) = local_source_volume_drift(spi_i) &
                          + ns_shape_drift * bigR * xjac * wst * delta_phi

                   else

                     local_source_volume_drift(spi_i) = 0.d0 ! Analytical volume will be used instead in neutral_source.f90

                   end if
                 end if

              end if

           end do
        end if
#endif

#if (defined WITH_Neutrals)
        !--- We calculate here the number of neutrals particles injected per second with n_particles_inj and the number of neutrals in the plasma

        source_neutral     = 0.d0
        source_neutral_arr = 0.d0

#if (defined WITH_Impurities)
        source_imp       = 0.d0; source_imp_arr       = 0.d0
        source_imp_drift = 0.d0; source_imp_drift_arr = 0.d0
        call total_imp_source(x_g(mp,ms,mt),y_g(mp,ms,mt),phi,ps0,source_neutral_arr,source_imp_arr,m_i_over_m_imp,index_main_imp, source_neutral_drift_arr, source_imp_drift_arr)
#else /* WITH_Impurities */
        call total_neutral_source(x_g(mp,ms,mt),y_g(mp,ms,mt),phi,ps0,source_neutral_arr,source_neutral_drift_arr)
#endif /* WITH_Impurities */ 

        do i_inj = 1,n_inj
          if (drift_distance(i_inj) /= 0.d0) then
            source_neutral = source_neutral + source_neutral_drift_arr(i_inj)
          else
            source_neutral = source_neutral + source_neutral_arr(i_inj)
          end if
        end do

        ! To detect NaNs
        if (source_neutral /= source_neutral) then
          write(*,*) 'ERROR in mod_integrals_3D: source_neutral = ', source_neutral
          stop
        end if

        source_neutral       = max(0.,source_neutral)

        ! Neutral injection rate in particles/s
        local_n_particles_inj = local_n_particles_inj + 0.5d0 * central_density * 1.d20 * source_neutral * bigR *&
                                 xjac * wst * delta_phi / sqrt(MU_ZERO*central_mass*ATOMIC_MASS_UNIT*central_density*1.d20)
        ! Total neutrals in particles
        local_n_particles     = local_n_particles     +  rn0 * central_density * 1.d20 * bigR * xjac * wst * delta_phi
        ! Frictional heat source
        fric_disp     =   0.5 * BigR**2 * (u0_x**2.0 + u0_y**2.0) * (r0_corr * rn0 * Sion_T)&
                        + 0.5 * vpar0**2 * BB2 * (r0_corr * rn0 * Sion_T)
        fric_disp_tot = fric_disp_tot + fric_disp * BigR * xjac * wst * delta_phi 
#endif
#ifdef WITH_Impurities
        ! --- Source of impurities (e.g. from MGI or SPI) and main ions (e.g. for mixed SPI)
        if (.not. (with_neutrals .and. with_impurities)) then ! if with_neutrals and with_impurities we should already have called this once above
          source_imp       = 0.d0; source_imp_arr       = 0.d0
          source_imp_drift = 0.d0; source_imp_drift_arr = 0.d0
        endif

        source_bg        = 0.d0; source_bg_arr       = 0.d0
        source_bg_drift  = 0.d0; source_bg_drift_arr = 0.d0

        if (.not. with_neutrals) call total_imp_source(x_g(mp,ms,mt),y_g(mp,ms,mt),phi,ps0,source_bg_arr,source_imp_arr,m_i_over_m_imp,index_main_imp, source_bg_drift_arr, source_imp_drift_arr) ! if with_neutrals and with_impurities we should already have called this once above

        do i_inj = 1,n_inj
          source_imp       = source_imp + source_imp_arr(i_inj)
          source_imp_drift = source_imp_drift + source_imp_drift_arr(i_inj)
          source_bg        = source_bg  + source_bg_arr(i_inj)
          source_bg_drift  = source_bg_drift + source_bg_drift_arr(i_inj)
        end do

        ! This is to detect N/A
        if (source_imp /= source_imp .or. source_bg /= source_bg) then
          write(*,*) "WARNING: source_imp = ", source_imp
          write(*,*) "WARNING: source_bg = ", source_bg
          stop
        end if
        if (source_imp_drift /= source_imp_drift .or. source_bg_drift /= source_bg_drift) then
          write(*,*) "WARNING: source_imp_drift = ", source_imp_drift
          write(*,*) "WARNING: source_bg_drift = ", source_bg_drift
          stop
        end if
        source_imp       = max(source_imp,0.d0)
        source_bg        = max(source_bg,0.d0)
        source_imp_drift = max(source_imp_drift,0.d0)
        source_bg_drift  = max(source_bg_drift,0.d0)

        ! Frictional heat source
        fric_disp     =   0.5 * BigR**2 * (u0_x**2.0 + u0_y**2.0) * (source_bg_drift + source_imp_drift)&
                        + 0.5 * vpar0**2 * BB2 * (source_bg_drift + source_imp_drift)
        fric_disp_tot = fric_disp_tot + fric_disp * BigR * xjac * wst * delta_phi 

        ! Neutral injection rate in particles/s
        local_n_particles_inj = local_n_particles_inj + 0.5d0 * central_density * 1.d20 * source_imp * m_i_over_m_imp * bigR &
	                         * xjac * wst * delta_phi / sqrt(MU_ZERO*central_mass*ATOMIC_MASS_UNIT*central_density*1.d20)
        ! Total neutrals in particles
        local_n_particles     = local_n_particles + central_density * 1.d20 * rimp0 * m_i_over_m_imp * bigR * xjac * wst * delta_phi
#endif

#if STELLARATOR_MODEL
        if (s_norm(ms,mt) <= 1.d0) then       ! Inside LCFS
#else
        if ( get_psi_n(psi_as_coord, y_g(mp,ms,mt)) <= 1.d0 ) then   !inside LCFS
#endif
#ifdef WITH_Impurities
          D_int = D_int + (r0-rimp0) * xjac * BigR * wst * delta_phi
#ifdef WITH_TiTe
          P_e_int = P_e_int + (r0+alpha_e*rimp0) * Te0 * xjac * BigR * wst * delta_phi
          P_i_int = P_i_int + (r0+alpha_i*rimp0) * Ti0 * xjac * BigR * wst * delta_phi
          P_int   = P_e_int + P_i_int
#else /* WITH_TiTe */
          P_int = P_int + (r0+alpha_imp*rimp0) * T0   * xjac * BigR * wst * delta_phi
          P_e_int = P_int / 2.
          P_i_int = P_e_int
#endif /* WITH_TiTe */
#else /* WITH_Impurities */
          D_int = D_int + r0        * xjac * BigR * wst * delta_phi
#ifdef WITH_TiTe
          P_e_int = P_e_int + r0 * Te0 * xjac * BigR * wst * delta_phi
          P_i_int = P_i_int + r0 * Ti0 * xjac * BigR * wst * delta_phi
          P_int   = P_e_int + P_i_int
#else /* WITH_TiTe */
          P_int = P_int + r0 * T0   * xjac * BigR * wst * delta_phi
          P_e_int = P_int / 2.
          P_i_int = P_e_int
#endif /* WITH_TiTe */
#endif /* WITH_Impurities */
#ifdef WITH_TiTe
		  !H_impl_int Te
          H_impl_int = H_impl_int +implicit_heat_source*(gamma-1.d0)   *xjac*BigR*wst*delta_phi&
               *( 0.5d0*Tie_min_neg* (1+exp( (min(Te0,Tie_min_neg)-Tie_min_neg)/(0.5d0*Tie_min_neg) )) -min(Te0,Tie_min_neg))
		  !H_impl_int Ti
          H_impl_int = H_impl_int +implicit_heat_source*(gamma-1.d0)  *xjac*BigR*wst*delta_phi & 
               *( 0.5d0*Tie_min_neg* (1+exp( (min(Ti0,Tie_min_neg)-Tie_min_neg)/(0.5d0*Tie_min_neg) )) -min(Ti0,Tie_min_neg))
#else /* WITH_TiTe */
		  !H_impl_int T0
          H_impl_int = H_impl_int + implicit_heat_source*(gamma-1.d0) *xjac*BigR*wst*delta_phi &
          *(0.5d0*T_min_neg * ( 1+exp( (min(T0,T_min_neg)-T_min_neg)/(0.5d0*T_min_neg) )) -min(T0,T_min_neg))
#endif /* WITH_TiTe */
          C_intern = C_intern - zj0 /BigR * xjac *        wst * delta_phi    ! 2D integral
          C_intern_3d = C_intern_3d - zj0 * xjac * wst * delta_phi ! 3D integral
          area1    = area1    +  xjac * wst * delta_phi         
          Vol_in   = Vol_in   +  xjac * BigR * wst * delta_phi
          H_int = H_int + heat_source     * xjac * BigR * wst * delta_phi
          S_int = S_int + particle_source * xjac * BigR * wst * delta_phi
          VP_int = VP_int + r0 * vpar0**2 * BB2 * xjac * BigR * wst * delta_phi
          local_mom_par_int = local_mom_par_int + r0 * vpar0 * sqrt(BB2) * xjac * BigR * wst * delta_phi
#if STELLARATOR_MODEL
          VK_int = VK_int + r0*(dudx**2 + dudy**2)*BigR**2*xjac*BigR*wst*delta_phi/F0**2
          VM_int = VM_int + (Bv2*(dpsidx**2 + dpsidy**2 + dpsidp**2/BigR**2) &
                 - (chi(1,0,0)*dpsidx + chi(0,1,0)*dpsidy + chi(0,0,1)*dpsidp/BigR**2)**2)*xjac*BigR*wst*delta_phi/F0**2
          VB_int  = VB_int  + 2.0*r0*T0 / (Bv2*(1.d0 + (dpsidx**2 + dpsidy**2 + dpsidp**2/BigR**2)/F0**2) &
                                     - ((chi(1,0,0)*dpsidx + chi(0,1,0)*dpsidy + chi(0,0,1)*dpsidp/BigR**2)**2)/F0**2) *xjac*BigR*wst*delta_phi
          mag_pres_int = mag_pres_int + (Bv2*(1.d0 + (dpsidx**2 + dpsidy**2 + dpsidp**2/BigR**2)/F0**2) &
                                      - ((chi(1,0,0)*dpsidx + chi(0,1,0)*dpsidy + chi(0,0,1)*dpsidp/BigR**2)**2)/F0**2)/2 *xjac*BigR*wst*delta_phi
#else
          VK_int = VK_int + r0 * (dudx**2 + dudy**2) * BigR**2 * xjac * BigR * wst * delta_phi
          VM_int = VM_int + (dpsidx**2+dpsidy**2)/BigR**2 * xjac * BigR * wst * delta_phi
          VB_int = VB_int + 2.0*r0*T0 / BB2 * xjac * BigR * wst * delta_phi
          mag_pres_int = mag_pres_int + BB2/2 * xjac * BigR * wst * delta_phi
#endif
          J2_int = J2_int + eta_T_ohm * (ZJ0/BigR)**2.d0 * xjac * BigR * wst * delta_phi

          if (use_ncs .or. use_ics) then 
            local_aux_mom_par_int=local_aux_mom_par_int+ aux_mom_par0 /sqrt(BB2)* xjac * BigR * wst * delta_phi !*sqrt(BB2)
          endif 

          
        else  ! not inside LCFS
#ifdef WITH_Impurities
          D_ext = D_ext + (r0-rimp0) * xjac * BigR * wst * delta_phi
#ifdef WITH_TiTe
          P_e_ext = P_e_ext + (r0+alpha_e*rimp0) * Te0 * xjac * BigR * wst * delta_phi
          P_i_ext = P_i_ext + (r0+alpha_i*rimp0) * Ti0 * xjac * BigR * wst * delta_phi
          P_ext   = P_e_ext + P_i_ext
#else /* WITH_TiTe */
          P_ext = P_ext + (r0+alpha_imp*rimp0) * T0   * xjac * BigR * wst * delta_phi
          P_e_ext = P_ext / 2.
          P_i_ext = P_e_ext
#endif /* WITH_TiTe */
#else /* WITH_Impurities */
          D_ext = D_ext + r0 * xjac * BigR * wst * delta_phi
#ifdef WITH_TiTe
          P_e_ext = P_e_ext + r0 * Te0 * xjac * BigR * wst * delta_phi
          P_i_ext = P_i_ext + r0 * Ti0 * xjac * BigR * wst * delta_phi
          P_ext   = P_e_ext + P_i_ext
#else /* WITH_TiTe */
          P_ext = P_ext + r0 * T0   * xjac * BigR * wst * delta_phi
          P_e_ext = P_ext / 2.
          P_i_ext = P_e_ext
#endif /* WITH_TiTe */
#endif /* WITH_Impurities */
#ifdef WITH_TiTe
		  !H_impl_ext Te
          H_impl_ext = H_impl_ext +implicit_heat_source*(gamma-1.d0)*xjac*BigR*wst*delta_phi & 
               *(0.5d0*Tie_min_neg * (1 + exp( (min(Te0,Tie_min_neg)-Tie_min_neg)/(0.5d0*Tie_min_neg) )) -min(Te0,Tie_min_neg))
		  !H_iml_ext Ti
          H_impl_ext = H_impl_ext +implicit_heat_source*(gamma-1.d0)*xjac*BigR*wst*delta_phi & 
               *(  0.5d0*Tie_min_neg*(1 + exp( (min(Ti0,Tie_min_neg)-Tie_min_neg)/(0.5d0*Tie_min_neg))) -min(Ti0,Tie_min_neg))
#else /* WITH_TiTe */
		  !H_impl_ext T0
          H_impl_ext = H_impl_ext + implicit_heat_source*(gamma-1.d0)*xjac*BigR*wst*delta_phi&
               *(0.5d0*T_min_neg * (1 + exp( (min(T0,T_min_neg)-T_min_neg)/(0.5d0*T_min_neg) ) ) -min(T0,T_min_neg))
#endif /* WITH_TiTe */
          C_ext = C_ext - zj0 / BigR * xjac *        wst * delta_phi  ! 2D integral
          C_ext_3d = C_ext_3d - zj0 * xjac * wst * delta_phi
          H_ext = H_ext + heat_source     * xjac * BigR * wst * delta_phi
          S_ext = S_ext + particle_source * xjac * BigR * wst * delta_phi
          Vol_ext   = Vol_ext   +           xjac * BigR * wst * delta_phi
          VP_ext = VP_ext + r0 * vpar0**2 * BB2 * xjac * BigR * wst * delta_phi
          local_mom_par_ext = local_mom_par_ext + r0 * vpar0 * sqrt(BB2) * xjac * BigR * wst * delta_phi
#if STELLARATOR_MODEL
          VK_ext = VK_ext + r0*(dudx**2 + dudy**2)*BigR**2*xjac*BigR*wst*delta_phi/F0**2
          VM_ext = VM_ext + (Bv2*(dpsidx**2 + dpsidy**2 + dpsidp**2/BigR**2) &
                 - (chi(1,0,0)*dpsidx + chi(0,1,0)*dpsidy + chi(0,0,1)*dpsidp/BigR**2)**2)*xjac*BigR*wst*delta_phi/F0**2
          VB_ext  = VB_ext + 2.0*r0*T0 / (Bv2*(1.d0 + (dpsidx**2 + dpsidy**2 + dpsidp**2/BigR**2)/F0**2) &
                                     - ((chi(1,0,0)*dpsidx + chi(0,1,0)*dpsidy + chi(0,0,1)*dpsidp/BigR**2)**2)/F0**2) *xjac*BigR*wst*delta_phi
          mag_pres_ext = mag_pres_ext + (Bv2*(1.d0 + (dpsidx**2 + dpsidy**2 + dpsidp**2/BigR**2)/F0**2) &
                                      - ((chi(1,0,0)*dpsidx + chi(0,1,0)*dpsidy + chi(0,0,1)*dpsidp/BigR**2)**2)/F0**2)/2 *xjac*BigR*wst*delta_phi
#else
          VK_ext = VK_ext + r0 * (dudx**2 + dudy**2) * BigR**2 * xjac * BigR * wst * delta_phi
          VM_ext = VM_ext + (dpsidx**2+dpsidy**2)/BigR**2 * xjac * BigR * wst * delta_phi
          VB_ext = VB_ext + 2.0*r0*T0 / BB2 * xjac * BigR * wst * delta_phi
          mag_pres_ext = mag_pres_ext + BB2/2 * xjac * BigR * wst * delta_phi
#endif
          J2_ext = J2_ext + eta_T_ohm * (ZJ0/BigR)**2.d0 * xjac * BigR * wst * delta_phi

          if (use_ncs .or. use_ics) then 
            local_aux_mom_par_ext=local_aux_mom_par_ext+ aux_mom_par0 /sqrt(BB2)* xjac * BigR * wst * delta_phi !*sqrt(BB2)
          endif  
         

        endif !in or outside LCFS

        
        BB2_zero = (F0 **2 + (dpsidx - dpsidx_3d) **2 + (dpsidy - dpsidy_3d) **2) / BigR ** 2
        ! SAW energy functional (linear MHD), see the first term of eq. (8.31) in Freidberg's Ideal MHD
        ! and/or the first term of eq. (2.18) in J. Plasma Phys. (2022), vol. 88, 905880512
        saw_ene_dens = (F0 **2 * (dpsidx_3d**2 + dpsidy_3d**2) + ((dpsidx - dpsidx_3d) **2 + (dpsidy - dpsidy_3d) **2) * (dpsidx_3d**2 + dpsidy_3d**2) & 
               - ((dpsidx - dpsidx_3d) * dpsidx_3d + (dpsidy - dpsidy_3d) * dpsidy_3d) ** 2) / (BigR**4 * BB2_zero)
        SAW_tot = SAW_tot + saw_ene_dens * xjac * BigR * wst * delta_phi
      enddo
    enddo
  enddo
  do iv = 1, n_vertex_max
    call dealloc_node(nodes(iv))
    call dealloc_node(aux_nodes(iv))
  enddo
enddo
!$omp end do
!$omp end parallel

!------ Calculate boundary fluxes --------------------------------------------------------
!--- go through the boundary elements
do m_bndelem = 1, bnd_elm_list%n_bnd_elements

  bndelem = bnd_elm_list%bnd_element(m_bndelem)
  elm_k   = element_list%element(bndelem%element)
  mv1     = bnd_elm_list%bnd_element(m_bndelem)%side
  m_elm   = bnd_elm_list%bnd_element(m_bndelem)%element

  !--- calculate values at gaussian points on the element
  x_g_1D(:,:)  = 0.d0; x_s_1D(:,:)  = 0.d0;  x_t_1D(:,:)    = 0.d0;
  y_g_1D(:,:)  = 0.d0; y_s_1D(:,:)  = 0.d0;  y_t_1D(:,:)    = 0.d0;

  eq_g_1D(:,:,:) = 0.d0; eq_s_1D(:,:,:) = 0.d0; delta_g_1D(:,:,:) = 0.d0;
  s_norm_1D(:,:) = 0.0; stel_current_source_1D(:,:) = 0.d0

  do k_vertex = 1, 2
    do k_dof = 1, 2
      k_node      = bndelem%vertex(k_vertex)
      k_dir       = bndelem%direction(k_vertex,k_dof)
      k_size      = bndelem%size(k_vertex,k_dof)
      call make_deep_copy_node(node_list%node(k_node), node_k)
      
      do mp=1,n_plane
        do in=1,n_coord_tor
          x_g_1D(mp,:)   = x_g_1D(mp,:)  + node_k%x(in,k_dir,1) * k_size * H1  (k_vertex,k_dof,:) *HZ_coord(in,mp)
          y_g_1D(mp,:)   = y_g_1D(mp,:)  + node_k%x(in,k_dir,2) * k_size * H1  (k_vertex,k_dof,:) *HZ_coord(in,mp)
          x_s_1D(mp,:)   = x_s_1D(mp,:)  + node_k%x(in,k_dir,1) * k_size * H1_s(k_vertex,k_dof,:) *HZ_coord(in,mp)
          y_s_1D(mp,:)   = y_s_1D(mp,:)  + node_k%x(in,k_dir,2) * k_size * H1_s(k_vertex,k_dof,:) *HZ_coord(in,mp)
        enddo
      enddo
#if STELLARATOR_MODEL
      do mp=1,n_plane
        s_norm_1D(mp,:) = s_norm_1D(mp,:) + node_k%r_tor_eq(k_dof)*k_size*H1(k_vertex,k_dof,:)
        do in=1,n_tor
          stel_current_source_1D(mp,:) = stel_current_source_1D(mp,:) + node_k%j_source(in,k_dir)*k_size*H1(k_vertex,k_dof,:)*HZ(in,mp)
        enddo
      enddo
#endif

      do k=1,n_var
        do mp=1, n_plane 
          do in=1, n_tor
            if (present(exclude_n0)) then 
              if (exclude_n0 .and. in == 1) then
                if (n_tor > 1) then
                  cycle
                else  
                  write(*,*) 'n_tor = 1 , cannot exclude axisymmetric part'
                endif
              endif  
            endif
            eq_g_1D(mp,k,:) = eq_g_1D(mp,k,:) + node_k%values(in,k_dir,k) * k_size * H1(k_vertex,k_dof,:)   * HZ(in,mp)
            eq_s_1D(mp,k,:) = eq_s_1D(mp,k,:) + node_k%values(in,k_dir,k) * k_size * H1_s(k_vertex,k_dof,:) * HZ(in,mp)

            delta_g_1D(mp,k,:) = delta_g_1D(mp,k,:) + node_k%deltas(in,k_dir,k) * k_size * H1(k_vertex,k_dof,:)   * HZ(in,mp)
          enddo
        enddo
      enddo

    end do
  end do

  call dealloc_node(node_k)


  !--- Find out correct sign of the normal (it has to point outwards the domain)
  !---------------------------------------------------------------------------------- 
  ! --- Calculate an inside point on the element to calculate the
  ! direction of bnd normals
  call basisfunctions(xgauss(2),xgauss(2), G)  
  R_c = 0.d0 ;  Z_c = 0.d0 
  mp=1
  do i = 1, n_vertex_max
    do j = 1, n_degrees
      node_k = node_list%node(elm_k%vertex(i)) 
      do in=1,n_coord_tor
        R_c    = R_c + node_k%x(1,j,1) * elm_k%size(i,j) * G(i,j) * HZ_coord(in,mp)
        Z_c    = Z_c + node_k%x(1,j,2) * elm_k%size(i,j) * G(i,j) * HZ_coord(in,mp)
      enddo
    enddo
  enddo  
  vec_inside = (/ R_c - x_g_1D(mp,2), Z_c - y_g_1D(mp,2) /)       ! vector pointing towards the domain
  grad_t     = (/ -y_s_1D(mp,2) , x_s_1D(mp,2) /)     ! gradient of the coordinate t (normal to the boundary here)
  sign_out   = -1.d0 * sign( 1.d0, ( vec_inside(1)*grad_t(1) + vec_inside(2)*grad_t(2) ) )  
  !--------------------------------------------------------------------------------

  !--- Integrate quantity on the element surface
  do ms=1, n_gauss

    s_or_t = xgauss(ms) 

    ! --- Which s and t values correspond to the current point and is the
    !     boundary element an s=const or t=const side of the 2D element?
    st = elm_coords(mv1, s_or_t)
    sg = st(1); tg = st(2)

    do mp=1, n_plane
      phi       = 2.d0*PI*float(mp-1)/float(n_plane) / float(n_period)
      call interp_RZP(node_list,element_list,m_elm,sg,tg,phi,R,R_s,R_t,R_phi,R_st,R_ss,R_tt,R_sp,R_tp,R_pp,Z,Z_s,Z_t,Z_phi,Z_st,Z_ss,Z_tt,Z_sp,Z_tp,Z_pp)
      
      if ((abs(x_g_1d(mp,ms) - R) .gt. 1d-6) .or. (abs(y_g_1d(mp,ms) - Z) .gt. 1d-6)) then
        write(*,'(A,2i3,4e16.8)') 'INTEGRALS3D : SOMETHING IS VERY WRONG ALONG THE BOUNDARY : ', m_elm, mv1, x_g_1d(mp,ms), R, y_g_1d(mp,ms), Z
      endif

      BigR   = R
      xjac   = R_s * Z_t - R_t * Z_s
      
      ! Calculate surface area contribution from covariant components
      cross_deriv = (/(R_t*sin(phi)*Z_phi-(R_phi*sin(phi)+R*cos(phi))*Z_t),   &
                    (-R_t*Z_phi*cos(phi)+(R_phi*cos(phi)-R*sin(phi))*Z_t),  &
                    (R_t * R)  /)
      dA = sqrt(cross_deriv(1)*cross_deriv(1) + cross_deriv(2)*cross_deriv(2) + cross_deriv(3)*cross_deriv(3)) 
      
      grad_t = (/ -y_s_1D(mp, ms) , x_s_1D(mp, ms) /)   ! --- normal vector to the boundary 

      ps0      = eq_g_1D(mp,var_psi ,ms)  !--- here sbnd is the direction along the boundary!!
      ps0_sbnd = eq_s_1D(mp,var_psi ,ms)
      u0_sbnd  = eq_s_1D(mp,var_u   ,ms)
      zj0      = eq_g_1D(mp,var_Zj  ,ms) 
      r0       = eq_g_1D(mp,var_rho ,ms) 
      r0_corr  = corr_neg_dens(r0)
      T0       = eq_g_1D(mp,var_T   ,ms) 
#ifdef WITH_TiTe
      Ti0      = eq_g_1D(mp,var_Ti,ms)
      Te0      = eq_g_1D(mp,var_Te,ms)
#else
      Ti0      = eq_g_1D(mp,var_T,ms) /2.d0
      Te0      = eq_g_1D(mp,var_T,ms) /2.d0
      T0_corr  = corr_neg_temp(T0) 
#endif
      Ti0_corr     =     corr_neg_temp(Ti0 * 2.d0) / 2.d0
      Te0_corr     =     corr_neg_temp(Te0 * 2.d0) / 2.d0
      dTe0_corr_dT =     dcorr_neg_temp_dT(Te0 * 2.d0)       

#ifdef WITH_Vpar
      vpar0    = eq_g_1D(mp,var_vpar,ms)
#else
      vpar0    = 0.d0
#endif

#if (defined WITH_Neutrals)
      rn0      = eq_g_1D(mp,var_rhon,ms)
      rn0_corr = corr_neg_dens(rn0) ! Correction for negative rn0
#else
      rn0      = 0.d0
      rn0_corr = 0.d0 
#endif
#if (defined WITH_Impurities)
      rimp0      = eq_g_1D(mp,var_rhoimp,ms)
      rimp0_corr = corr_neg_dens1(rimp0) ! Correction for negative rimp0
#else
      rimp0      = 0.d0
      rimp0_corr = 0.d0 
#endif

      if (keep_current_prof) then
#if STELLARATOR_MODEL
        current_source = stel_current_source_1D(mp, ms)
#else
        call current(xpoint, xcase, R, Z, Z_xpoint, ps0, psi_axis, psi_bnd, current_source)
#endif
      else
        current_source = 0.d0
      endif
 
      !--- calculate derivates in real s, t (s_1D is a coordinate that can be s or t)
      psi_s  = 0.d0; psi_t  = 0.d0;
      u_s    = 0.d0; u_t    = 0.d0; u_p = 0.d0;
      rho_s  = 0.d0; rho_t  = 0.d0;
      rhon_s = 0.d0; rhon_t = 0.d0;
      rhoimp_s = 0.d0; rhoimp_t = 0.d0;
      T_s    = 0.d0; T_t    = 0.d0;
      Ti_s   = 0.d0; Ti_t   = 0.d0;
      Te_s   = 0.d0; Te_t   = 0.d0;
      vpar_s = 0.d0; vpar_t = 0.d0; 

      do in = 1,n_tor
        if (present(exclude_n0)) then
          if (exclude_n0 .and. in == 1) then
            if (n_tor > 1) then
              cycle
            else
              write(*,*) 'n_tor = 1 , cannot exclude axisymmetric part'
            endif
          endif
        endif
        call interp(node_list,element_list,m_elm,var_psi,in,sg,tg,PS,PS_s,PS_t,PS_st,PS_ss,PS_tt)
        psi_s = psi_s + PS_s * HZ(in,mp)
        psi_t = psi_t + PS_t * HZ(in,mp)
        
        if ( var_u /= 0 ) then
          call interp(node_list,element_list,m_elm,var_u,in,sg,tg,UU,UU_s,UU_t,UU_st,UU_ss,UU_tt)
          u_s   = u_s   + UU_s * HZ(in,mp)
          u_t   = u_t   + UU_t * HZ(in,mp)
          u_p   = u_p   + UU   * HZ_p(in,mp)
        else
          u_s = 0.d0
          u_t = 0.d0
          u_p = 0.d0
        end if

        call interp(node_list,element_list,m_elm,var_rho,in,sg,tg,RH,RH_s,RH_t,RH_st,RH_ss,RH_tt)
        rho_s = rho_s + RH_s * HZ(in,mp)
        rho_t = rho_t + RH_t * HZ(in,mp)

#ifdef WITH_TiTe
        call interp(node_list,element_list,m_elm,var_Ti,in,sg,tg,TT,TT_s,TT_t,TT_st,TT_ss,TT_tt)
        Ti_s = Ti_s + TT_s * HZ(in,mp)
        Ti_t = Ti_t + TT_t * HZ(in,mp)
        call interp(node_list,element_list,m_elm,var_Te,in,sg,tg,TT,TT_s,TT_t,TT_st,TT_ss,TT_tt)
        Te_s = Te_s + TT_s * HZ(in,mp)
        Te_t = Te_t + TT_t * HZ(in,mp)
        T_s = Te_s + Ti_s
        T_t = Te_t + Ti_t
#else
        call interp(node_list,element_list,m_elm,var_T,in,sg,tg,TT,TT_s,TT_t,TT_st,TT_ss,TT_tt)
        T_s = T_s + TT_s * HZ(in,mp)
        T_t = T_t + TT_t * HZ(in,mp)
        Te_s = T_s / 2.0
        Te_t = T_t / 2.0
        Ti_s = T_s / 2.0
        Ti_t = T_t / 2.0
#endif

#ifdef WITH_Vpar
        call interp(node_list,element_list,m_elm,var_Vpar,in,sg,tg,vp,vp_s,vp_t,vp_st,vp_ss,vp_tt)
        vpar_s = vpar_s + vp_s * HZ(in,mp)
        vpar_t = vpar_t + vp_t * HZ(in,mp)
#else
        vpar_s = 0.d0
        vpar_t = 0.d0
#endif

#if (defined WITH_Neutrals)
        call interp(node_list,element_list,m_elm,var_rhon,in,sg,tg,rn,rn_s,rn_t,rn_st,rn_ss,rn_tt)
        rhon_s = rhon_s + rn_s * HZ(in,mp)
        rhon_t = rhon_t + rn_t * HZ(in,mp)
#else
        rhon_s = 0.d0
        rhon_t = 0.d0
#endif
#if (defined WITH_Impurities)
        call interp(node_list,element_list,m_elm,var_rhoimp,in,sg,tg,rimp,rimp_s,rimp_t,rimp_st,rimp_ss,rimp_tt)
        rhoimp_s = rhoimp_s + rimp_s * HZ(in,mp)
        rhoimp_t = rhoimp_t + rimp_t * HZ(in,mp)
#else
        rhoimp_s = 0.d0
        rhoimp_t = 0.d0
#endif

      enddo

      dTedx  = (   Z_t * Te_s  - Z_s * Te_t  ) / xjac
      dTedy  = ( - R_t * Te_s  + R_s * Te_t  ) / xjac
      dTidx  = (   Z_t * Ti_s  - Z_s * Ti_t  ) / xjac
      dTidy  = ( - R_t * Ti_s  + R_s * Ti_t  ) / xjac
      dTdx   = (   Z_t * T_s   - Z_s * T_t   ) / xjac
      dTdy   = ( - R_t * T_s   + R_s * T_t   ) / xjac
      drhodx = (   Z_t * rho_s - Z_s * rho_t ) / xjac
      drhody = ( - R_t * rho_s + R_s * rho_t ) / xjac

      dpsidx = (   Z_t * psi_s - Z_s * psi_t ) / xjac
      dpsidy = ( - R_t * psi_s + R_s * psi_t ) / xjac

      vpar_x = (   Z_t * vpar_s - Z_s * vpar_t ) / xjac
      vpar_y = ( - R_t * vpar_s + R_s * vpar_t ) / xjac

      drhondx = (   Z_t * rhon_s - Z_s * rhon_t ) / xjac
      drhondy = ( - R_t * rhon_s + R_s * rhon_t ) / xjac

      drhoimpdx = (   Z_t * rhoimp_s - Z_s * rhoimp_t ) / xjac
      drhoimpdy = ( - R_t * rhoimp_s + R_s * rhoimp_t ) / xjac

      BB2    = (F0*F0 + dpsidx*dpsidx + dpsidy*dpsidy) / BigR**2
      Bnorm  = (dpsidx * grad_t(1) - dpsidy * grad_t(2)) / BigR   ! normal magnetic field to the JOREK boundary

      ! --- get normalized flux 
#if STELLARATOR_MODEL
      psi_n = s_norm_1D(mp,ms)
#else
      psi_n = get_psi_n(ps0,Z)
#endif

#ifdef WITH_TiTe
      if (use_zkperp_times_density) then 
        ZK_e_prof     = get_zk_eperp(psi_n) * max(r0,zkperp_density_floor)
        ZK_i_prof     = get_zk_iperp(psi_n) * max(r0,zkperp_density_floor)
      else
        ZK_e_prof     = get_zk_eperp(psi_n)
        ZK_i_prof     = get_zk_iperp(psi_n)
      end if

      ! --- Temperature dependent parallel heat conductivity
      call conductivity_parallel(ZK_i_par, ZK_par_max, Ti0, Ti0_corr, Ti_min_ZKpar, Ti_0,  & 
                                 ZK_i_par_T, ZK_i_par_neg_thresh, ZK_i_par_neg)
      call conductivity_parallel(ZK_e_par, ZK_par_max, Te0, Te0_corr, Te_min_ZKpar, Te_0,  &
                                 ZK_e_par_T, ZK_e_par_neg_thresh, ZK_e_par_neg)

#else
      if (use_zkperp_times_density) then
        ZK_prof = get_zkperp(psi_n) * max(r0,zkperp_density_floor)
      else
        ZK_prof = get_zkperp(psi_n)
      end if

      ! --- Temperature dependent parallel heat conductivity
      call conductivity_parallel(ZK_par, ZK_par_max, T0, T0_corr, T_min_ZKpar, T_0, &
                                 ZKpar_T, ZK_par_neg_thresh, ZK_par_neg)

#endif

      if ( with_TiTe ) then ! (with_TiTe) ****************************************************
        if (Ti0 .lt. ZK_i_prof_neg_thresh) then
          if (use_zkperp_times_density) then
            ZK_i_prof = ZK_i_prof_neg * max(r0,zkperp_density_floor)
          else
            ZK_i_prof = ZK_i_prof_neg
          end if
        end if
        if (Te0 .lt. ZK_e_prof_neg_thresh) then
          if (use_zkperp_times_density) then
            ZK_e_prof = ZK_e_prof_neg * max(r0,zkperp_density_floor)
          else
            ZK_e_prof = ZK_e_prof_neg
          end if
        end if
      else ! (with_TiTe = .f.), i.e. with single temperature ***************************************
        if (T0 .lt. ZK_prof_neg_thresh) then
          if (use_zkperp_times_density) then
            ZK_prof = ZK_prof_neg * max(r0,zkperp_density_floor)
          else
            ZK_prof = ZK_prof_neg
          end if
        end if
      endif ! (with_TiTe) ********************************************************************


#ifdef WITH_Impurities
      !-------------------------------------------
      ! Atomic physics parameters for Impurities
      !-------------------------------------------

      select case ( trim(imp_type(index_main_imp)) )
        case('D2')
          m_i_over_m_imp = central_mass/2.  ! Deuterium mass = 2 u
        case('Ar')
          m_i_over_m_imp = central_mass/40. ! Argon mass = 40 u
        case('Ne')
          m_i_over_m_imp = central_mass/20. ! Neon mass = 20 u
        case default
          write(*,*) '!! Gas type "', trim(imp_type(index_main_imp)), '" unknown (in mod_injection_source.f90) !!'
          write(*,*) '=> We assume the gas is D2.'
          m_i_over_m_imp = central_mass/2.
      end select

      Te_corr_eV = Te0_corr/(EL_CHG*MU_ZERO*central_density*1.d20)
      Te_eV = Te0/(EL_CHG*MU_ZERO*central_density*1.d20)
   
      if (allocated(P_imp)) deallocate(P_imp)
      allocate(P_imp(0:imp_adas(index_main_imp)%n_Z))
      call imp_cor(index_main_imp)%interp_linear(density=20.,temperature=log10(Te_corr_eV*EL_CHG/K_BOLTZ),&
                                    p_out=P_imp,z_avg=Z_imp,z_avg_Te=dZ_imp_dT)
      ! Convert gradient in T(K) in to gradient in T (eV)
      dZ_imp_dT = dZ_imp_dT *EL_CHG / K_BOLTZ
      ! Derivative wrt to T, with T in JOREK units
      dZ_imp_dT = dZ_imp_dT / (EL_CHG*MU_ZERO*central_density*1.d20)
      dZ_imp_dT = dZ_imp_dT * dTe0_corr_dT
#ifdef WITH_TiTe
      alpha_i       = m_i_over_m_imp - 1.
      alpha_e       = m_i_over_m_imp*Z_imp - 1.
      dalpha_e_dT   = m_i_over_m_imp*dZ_imp_dT

      ne_SI        = (r0_corr + alpha_e * rimp0_corr) * 1.d20 * central_density ! electron density (SI)
      ne_JOREK     = r0_corr + alpha_e * rimp0_corr ! Electron density in JOREK unit
      ne_JOREK     = corr_neg_dens(ne_JOREK,(/1.d-1,1.d-1/),1.d-3) ! Correction for negative electron density
                                                          ! Too small rho_1 will cause a problem
      dne_JOREK_dx = drhodx + alpha_e * drhoimpdx + rimp0_corr * dalpha_e_dT * dTedx
      dne_JOREK_dy = drhody + alpha_e * drhoimpdy + rimp0_corr * dalpha_e_dT * dTedy
#else /* WITH_TiTe */
      alpha_i       = m_i_over_m_imp - 1.
      alpha_e       = m_i_over_m_imp*Z_imp - 1.
      dalpha_e_dT   = m_i_over_m_imp*dZ_imp_dT
      alpha_imp    = 0.5*m_i_over_m_imp*(Z_imp+1.) - 1.
      beta_imp     = m_i_over_m_imp*Z_imp - 1.
      dbeta_imp_dT = m_i_over_m_imp*dZ_imp_dT
      ne_SI        = (r0_corr + beta_imp * rimp0_corr) * 1.d20 * central_density !electron density (SI)
      ne_JOREK     = r0_corr + beta_imp * rimp0_corr ! Electron density in JOREK unit
      ne_JOREK     = corr_neg_dens(ne_JOREK,(/1.d-1,1.d-1/),1.d-3) ! Correction for negative electron density
                                                             ! Too small rho_1 will cause a problem
      dne_JOREK_dx = drhodx + alpha_imp * drhoimpdx + rimp0_corr * dbeta_imp_dT * dTedx
      dne_JOREK_dy = drhody + alpha_imp * drhoimpdy + rimp0_corr * dbeta_imp_dT * dTedy
#endif /* WITH_TiTe */

      ! Calculate the effective charge of all species
      Z_eff        = 0.

      ! First get the value of Z_eff
      Z_eff        = r0_corr - rimp0_corr
      do ion_i=1, imp_adas(index_main_imp)%n_Z
        Z_eff      = Z_eff + m_i_over_m_imp * rimp0_corr * P_imp(ion_i) * real(ion_i,8)**2
      end do
      Z_eff        = Z_eff / ne_JOREK

      if (Z_eff < 1) Z_eff = 1.
      if (Z_eff > imp_adas(1)%n_Z)  Z_eff = imp_adas(1)%n_Z

      dPedx  = ne_JOREK * dTedx + Te0 * dne_JOREK_dx
      dPedy  = ne_JOREK * dTedy + Te0 * dne_JOREK_dy
      dPidx  = (r0 + alpha_i*rimp0) * dTidx + Ti0 * (drhodx + alpha_i*drhoimpdx)
      dPidy  = (r0 + alpha_i*rimp0) * dTidy + Ti0 * (drhody + alpha_i*drhoimpdy)
      dPdx   = dPedx + dPidx
      dPdy   = dPedy + dPidy
#else /* WITH_Impurities */
      dPdx   = r0 * dTdx + T0 * drhodx
      dPdy   = r0 * dTdy + T0 * drhody
#endif /* WITH_Impurities */

      D_prof  = get_dperp (psi_n)

      pflow       = - gamma/(gamma-1.d0) * r0 * T0 * vpar0 * ps0_sbnd * sign_out 
      kinflow     = - 0.5d0*r0*vpar0**3.d0*BB2* ps0_sbnd * sign_out 

#ifdef WITH_TiTe
      cond_par    =   ZK_e_par_T *(dTedx * dpsidy - dTedy * dpsidx) /BigR/BB2 * ps0_sbnd * sign_out / (gamma-1.d0)&
                    + ZK_i_par_T *(dTidx * dpsidy - dTidy * dpsidx) /BigR/BB2 * ps0_sbnd * sign_out / (gamma-1.d0) 
      cond_perp   = - ZK_e_prof *(dTedx*grad_t(1) + dTedy*grad_t(2)) * BigR              * sign_out / (gamma-1.d0)&
                    - ZK_e_prof *(dTedx * dpsidy  - dTedy * dpsidx) /BigR/BB2 * ps0_sbnd * sign_out / (gamma-1.d0)&
                    - ZK_i_prof *(dTidx*grad_t(1) + dTidy*grad_t(2)) * BigR              * sign_out / (gamma-1.d0)&
                    - ZK_i_prof *(dTidx * dpsidy  - dTidy * dpsidx) /BigR/BB2 * ps0_sbnd * sign_out / (gamma-1.d0)
#else
      cond_par    =   ZKpar_T *( dTdx * dpsidy  - dTdy * dpsidx ) /BigR/BB2 * ps0_sbnd * sign_out / (gamma-1.d0) 
      cond_perp   = - ZK_prof *( dTdx*grad_t(1) + dTdy*grad_t(2)) * BigR               * sign_out / (gamma-1.d0) &
                    - ZK_prof *( dTdx * dpsidy  - dTdy * dpsidx ) /BigR/BB2 * ps0_sbnd * sign_out / (gamma-1.d0) 
#endif

      Dpar_part_flow    =   D_par  * (drhodx*dpsidy    - drhody*dpsidx    )/BigR/BB2 * ps0_sbnd * sign_out 
      Dperp_part_flow   = - D_prof * (drhodx*grad_t(1) + drhody*grad_t(2) ) * BigR              * sign_out &                               
                          - D_prof * (drhodx*dpsidy    - drhody*dpsidx    )/BigR/BB2 * ps0_sbnd * sign_out

      vpar_part_flow    =  - r0 *  vpar0     * ps0_sbnd * sign_out 
      vperp_part_flow   =    r0 * BigR**2.d0 * u0_sbnd  * sign_out 

      neut_part_flow    = - (D_neutral_x*drhondx*grad_t(1) + D_neutral_y*drhondy*grad_t(2)) * BigR  * sign_out

      viscopar_f  = visco_par * (F0/BigR)**2.d0 *  (vpar_x*grad_t(1) + vpar_y *grad_t(2) ) &
                  * sign_out  * BigR * vpar0

!      dpsi_dt     = BigR*(psi_s*u_t - psi_t*u_s)/xjac + eta_T*(zj0-current_source) - F0*u_p 
      dpsi_dt      = delta_g_1D(mp, var_psi, ms) / tstep  
      poynting_tmp = dpsi_dt * (dpsidx*grad_t(1) + dpsidy*grad_t(2)) * sign_out / BigR 


      vn_p0         = vn_p0          +   pflow      * wgauss(ms) * delta_phi 
      kinpar_flux   = kinpar_flux    + kinflow      * wgauss(ms) * delta_phi 
      qn_par        = qn_par         + cond_par     * wgauss(ms) * delta_phi 
      qn_perp       = qn_perp        + cond_perp    * wgauss(ms) * delta_phi 

      Dpar_part_flux   =  Dpar_part_flux     + Dpar_part_flow    * wgauss(ms) * delta_phi 
      Dperp_part_flux  =  Dperp_part_flux    + Dperp_part_flow   * wgauss(ms) * delta_phi 
      vpar_part_flux   =  vpar_part_flux     + vpar_part_flow    * wgauss(ms) * delta_phi 
      vperp_part_flux  =  vperp_part_flux    + vperp_part_flow   * wgauss(ms) * delta_phi  
      neut_part_flux   =  neut_part_flux     + neut_part_flow    * wgauss(ms) * delta_phi 

      viscopar_flux = viscopar_flux  +  viscopar_f  * wgauss(ms) * delta_phi
      poynting_flux = poynting_flux  + poynting_tmp * wgauss(ms) * delta_phi

      int_B_norm = int_B_norm +  Bnorm**2 / BB2 * sqrt(x_s_1D(mp,ms)**2 + y_s_1D(mp,ms)**2) * wgauss(ms) * delta_phi  
      surface_area      = surface_area       + dA * wgauss(ms) * delta_phi
    enddo
    mp = 1
    L = L + sqrt(x_s_1D(mp,ms)**2 + y_s_1D(mp,ms)**2) * wgauss(ms)
  enddo

enddo !--- bnd elements, end of calculation of boundary fluxes

! --- gather contribution from all MPI processes
#ifndef NOMPIVERSION
call MPI_AllReduce(D_int,density_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(D_ext,density_out,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(P_int,pressure_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(P_ext,pressure_out,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(P_e_int,pressure_e_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(P_e_ext,pressure_e_out,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(P_i_int,pressure_i_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(P_i_ext,pressure_i_out,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(mag_pres_tot,mag_pressure,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(mag_pres_int,mag_pressure_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(mag_pres_ext,mag_pressure_out,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(C_intern,current_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(C_ext,current_out,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(C_intern_3d,current_R_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(C_ext_3d,current_R_out,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(R2curr_tmp,      R2curr,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(Zcurr_tmp , Z_curr_cent,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(Vol_in,Volume_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(Vol_ext,Volume_ext,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(area1,area,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(D_tot,density_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(P_tot,pressure,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(P_e_tot,pressure_e,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(P_i_tot,pressure_i,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(H_ext,heating_out,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(H_int,heating_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(H_impl_ext,heating_impl_out,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(H_impl_int,heating_impl_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(S_ext,source_out,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(S_int,source_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(VP_int,kin_par_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(VP_ext,kin_par_out,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(VP_tot,kin_par_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(VK_int,kin_perp_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(VK_ext,kin_perp_out,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(VK_tot,kin_perp_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(VM_int,mag_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(VM_ext,mag_out,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(VM_tot,mag_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(VB_tot,beta_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(VB_int,beta_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(VB_ext,beta_out,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(SAW_tot,saw_energy_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(J2_int,ohm_in,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(J2_ext,ohm_out,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(J2_tot,ohm_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(heli_tot, helicity_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(thm_wk_tot, thermal_work_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(mag_wk_tot, mag_work_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(vpar_disp_tot, viscopar_dissip_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(vprp_disp_tot, visco_dissip_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(fric_disp_tot, friction_dissip_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(mag_src_tot, mag_source_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(varmin,V_min,n_var,MPI_DOUBLE_PRECISION,MPI_MIN,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(varmax,V_max,n_var,MPI_DOUBLE_PRECISION,MPI_MAX,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(momentum_x,Px,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(momentum_y,Py,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)

call MPI_AllReduce(local_Nion,Nion,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_Nrec,Nrec,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
#ifdef WITH_TiTe
call MPI_AllReduce(local_pn_e,plasmaneutral_e,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_pn_i,plasmaneutral_i,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
#else
call MPI_AllReduce(local_pn,plasmaneutral,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
#endif
call MPI_AllReduce(local_Prec,Prec,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_Prb,Prb,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_Prb_cooling,Prb_cooling,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_mom_par_int,mom_par_int,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_mom_par_ext,mom_par_ext,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_mom_par_tot,mom_par_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_aux_mom_par_int,aux_mom_par_int,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_aux_mom_par_ext,aux_mom_par_ext,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_aux_mom_par_tot,aux_mom_par_tot,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)

#if (defined WITH_Neutrals) || (defined WITH_Impurities)
call MPI_AllReduce(local_radiation, total_radiation,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_radiation_cooling, total_radiation_cooling,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_radiation_bg, total_radiation_bg,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_E_ion, total_E_ion,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_P_ei, total_P_ei,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_P_ion, total_P_ion,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_radiation_phi, total_radiation_phi,n_plane,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
call MPI_AllReduce(local_radiation_cooling_phi, total_radiation_cooling_phi,n_plane,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
#endif /* (defined WITH_Neutrals) || (defined WITH_Impurities) */

#else /* NOMPIVERSION */
density_in           = D_int
density_out          = D_ext
pressure_in          = P_int
pressure_out         = P_ext
pressure_e_in        = P_e_int
pressure_e_out       = P_e_ext
pressure_i_in        = P_i_int
pressure_i_out       = P_i_ext
mag_pressure         = mag_pres_tot
mag_pressure_in      = mag_pres_int
mag_pressure_out     = mag_pres_ext
current_in           = C_intern
current_out          = C_ext
current_R_in         = C_intern_3d
current_R_out        = C_ext_3d
R2curr               = R2curr_tmp
Z_curr_cent          = Zcurr_tmp
Volume_in            = Vol_in
Volume_ext           = Vol_ext
area                 = area1
density_tot          = D_tot
pressure             = P_tot
pressure_e           = P_e_tot
pressure_i           = P_i_tot
heating_out          = H_ext
heating_in           = H_int
heating_impl_out     = H_impl_ext
heating_impl_in      = H_impl_int
source_out           = S_ext
source_in            = S_int
kin_par_in           = VP_int
kin_par_out          = VP_ext
kin_par_tot          = VP_tot
kin_perp_in          = VK_int
kin_perp_out         = VK_ext
kin_perp_tot         = VK_tot
mag_in               = VM_int
mag_out              = VM_ext
mag_tot              = VM_tot
beta_tot             = VB_tot
beta_in              = VB_int
beta_out             = VB_ext
saw_energy_tot       = SAW_tot
ohm_in               = J2_int
ohm_out              = J2_ext
ohm_tot              = J2_tot
helicity_tot         = heli_tot
thermal_work_tot     = thm_wk_tot
mag_work_tot         = mag_wk_tot
visco_dissip_tot     = vprp_disp_tot
viscopar_dissip_tot  = vpar_disp_tot
friction_dissip_tot  = fric_disp_tot
mag_source_tot       = mag_src_tot
V_min                = varmin
V_max                = varmax
Px                   = momentum_x
Py                   = momentum_y

mom_par_int = local_mom_par_int
mom_par_ext = local_mom_par_ext
mom_par_tot = local_mom_par_tot
Nion                 = local_Nion
Nrec                 = local_Nrec
#ifdef WITH_TiTe
plasmaneutral_e        = local_pn_e
plasmaneutral_i        = local_pn_i
#else
plasmaneutral        = local_pn
#endif
Prec                 = local_Prec
Prb                  = local_Prb
Prb_cooling          = local_Prb_cooling
aux_mom_par_int = local_aux_mom_par_int
aux_mom_par_ext = local_aux_mom_par_ext
aux_mom_par_tot = local_aux_mom_par_tot

#if (defined WITH_Neutrals) || (defined WITH_Impurities)
total_radiation             = local_radiation
total_radiation_cooling     = local_radiation_cooling
total_radiation_bg          = local_radiation_bg
total_E_ion                 = local_E_ion
total_P_ei                  = local_P_ei
total_P_ion                 = local_P_ion
total_radiation_phi         = local_radiation_phi
total_radiation_cooling_phi = local_radiation_cooling_phi 
#endif /* (defined WITH_Neutrals) || (defined WITH_Impurities) */
#endif /* NOMPIVERSION */

if (use_pellet) then
#ifndef NOMPIVERSION
  call MPI_AllReduce(local_pellet_particles,total_pellet_particles,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
  call MPI_AllReduce(local_plasma_particles,total_plasma_particles,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
  call MPI_AllReduce(local_pellet_volume,total_pellet_volume,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
#else /* NOMPIVERSION */
  total_pellet_particles = local_pellet_particles
  total_plasma_particles = local_plasma_particles
  total_pellet_volume    = local_pellet_volume
#endif /* NOMPIVERSION */
endif

#if (defined WITH_Neutrals) || (defined WITH_Impurities)
if (using_spi) then   
   do spi_i=1, n_spi_tot
#ifndef NOMPIVERSION
      call MPI_AllReduce(local_source_volume(spi_i),pellets(spi_i)%spi_vol,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
      call MPI_AllReduce(local_source_volume_drift(spi_i),pellets(spi_i)%spi_vol_drift,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
#else /* NOMPIVERSION */
      pellets(spi_i)%spi_vol = local_source_volume(spi_i)
      pellets(spi_i)%spi_vol_drift = local_source_volume_drift(spi_i)
#endif /* NOMPIVERSION */
   end do
   deallocate(local_source_volume)
   deallocate(local_source_volume_drift)
end if
if (allocated(local_source_volume)) deallocate(local_source_volume) !In case of dummy array
if (allocated(local_source_volume_drift)) deallocate(local_source_volume_drift)
#endif

#if (defined WITH_Neutrals) || (defined WITH_Impurities)
#ifndef NOMPIVERSION
  call MPI_AllReduce(local_n_particles_inj, total_n_particles_inj,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
  call MPI_AllReduce(local_n_particles, total_n_particles,1,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,ierr)
#else /* NOMPIVERSION */
  total_n_particles_inj = local_n_particles_inj
  total_n_particles     = local_n_particles
#endif /* NOMPIVERSION */
  neut_particles_tot    = total_n_particles / (central_density * 1.d20)
#else
  neut_particles_tot = 0.d0
#endif

! --- Normalization factors
rho_norm = central_density*1.d20 * central_mass * ATOMIC_MASS_UNIT 
t_norm   = sqrt(MU_zero*rho_norm)

if (units == SI_UNITS) then
  fact_mu0  = 1.d0/mu_zero
  fact_flux = 1.d0/(mu_zero*t_norm)  
  t_norm2   = t_norm
  fact_part = central_density * 1.d20
else
  fact_mu0  = 1.d0
  fact_flux = 1.d0
  t_norm2   = 1.d0
  fact_part = 1.d0
endif

if (n_tor .eq. 1) then
  Px = 0.d0
  Py = 0.d0
end if

! --- Volume integrals
current_in           = n_period * current_in  * fact_mu0  / (2.d0 * PI)
current_out          = n_period * current_out * fact_mu0  / (2.d0 * PI)
current_R_in         = n_period * current_R_in  * fact_mu0  / (2.d0 * PI)
current_R_out        = n_period * current_R_out * fact_mu0  / (2.d0 * PI)
R2curr               = n_period * R2curr      * fact_mu0  / (2.d0 * PI)
Z_curr_cent          = n_period * Z_curr_cent * fact_mu0  / (2.d0 * PI)
pressure             = n_period * pressure    * fact_mu0  / (GAMMA-1.d0)
pressure_in          = n_period * pressure_in * fact_mu0  / (GAMMA-1.d0)
pressure_out         = n_period * pressure_out* fact_mu0  / (GAMMA-1.d0)
pressure_e           = n_period * pressure_e  * fact_mu0  / (GAMMA-1.d0)
pressure_e_in        = n_period * pressure_e_in * fact_mu0  / (GAMMA-1.d0)
pressure_e_out       = n_period * pressure_e_out* fact_mu0  / (GAMMA-1.d0)
pressure_i           = n_period * pressure_i    * fact_mu0  / (GAMMA-1.d0)
pressure_i_in        = n_period * pressure_i_in * fact_mu0  / (GAMMA-1.d0)
pressure_i_out       = n_period * pressure_i_out* fact_mu0  / (GAMMA-1.d0)
mag_pressure         = n_period * mag_pressure    * fact_mu0 / (GAMMA-1.d0)
mag_pressure_in      = n_period * mag_pressure_in * fact_mu0 / (GAMMA-1.d0)
mag_pressure_out     = n_period * mag_pressure_out* fact_mu0 / (GAMMA-1.d0)
kin_par_tot          = n_period * kin_par_tot * fact_mu0  * 0.5d0
kin_par_in           = n_period * kin_par_in  * fact_mu0  * 0.5d0
kin_par_out          = n_period * kin_par_out * fact_mu0  * 0.5d0
kin_perp_tot         = n_period * kin_perp_tot* fact_mu0  * 0.5d0
kin_perp_in          = n_period * kin_perp_in * fact_mu0  * 0.5d0
kin_perp_out         = n_period * kin_perp_out* fact_mu0  * 0.5d0
mag_tot              = n_period * mag_tot     * fact_mu0  * 0.5d0
mag_in               = n_period * mag_in      * fact_mu0  * 0.5d0
mag_out              = n_period * mag_out     * fact_mu0  * 0.5d0
saw_energy_tot       = n_period * saw_energy_tot * fact_mu0  * 0.5d0
ohm_tot              = n_period * ohm_tot     * fact_flux
ohm_in               = n_period * ohm_in      * fact_flux
ohm_out              = n_period * ohm_out     * fact_flux
heating_out          = n_period * heating_out * fact_flux / (GAMMA-1.d0)
heating_in           = n_period * heating_in  * fact_flux / (GAMMA-1.d0)
heating_impl_out     = n_period * heating_impl_out * fact_flux / (GAMMA-1.d0)
heating_impl_in      = n_period * heating_impl_in  * fact_flux / (GAMMA-1.d0)
source_out           = n_period * source_out  * fact_part / t_norm2
source_in            = n_period * source_in   * fact_part / t_norm2
density_tot          = n_period * density_tot * fact_part 
density_in           = n_period * density_in  * fact_part
density_out          = n_period * density_out * fact_part
neut_particles_tot   = n_period * neut_particles_tot * fact_part
helicity_tot         = n_period * helicity_tot
thermal_work_tot     = n_period * thermal_work_tot    * fact_flux 
mag_work_tot         = n_period * mag_work_tot        * fact_flux
visco_dissip_tot     = n_period * visco_dissip_tot    * fact_flux
viscopar_dissip_tot  = n_period * viscopar_dissip_tot * fact_flux
friction_dissip_tot  = n_period * friction_dissip_tot * fact_flux
mag_source_tot       = n_period * mag_source_tot      * fact_flux
Volume_in            = n_period * Volume_in
Volume_ext           = n_period * Volume_ext
area                 = n_period * area / (2.d0 * PI)
surface_area         = n_period * surface_area
beta_tot             = n_period * beta_tot / (Volume_in + Volume_ext)
beta_in              = n_period * beta_in  / Volume_in
if (volume_ext .ne. 0) then
  beta_out           = n_period * beta_out / Volume_ext
else
  beta_out           = 0.d0
endif
mom_par_int      = n_period * mom_par_int * rho_norm / t_norm
mom_par_ext      = n_period * mom_par_ext * rho_norm / t_norm
mom_par_tot      = n_period * mom_par_tot * rho_norm / t_norm

Nion                 = n_period * Nion         * fact_part / t_norm2
Nrec                 = n_period * Nrec         * fact_part / t_norm2
#ifdef WITH_TiTe
plasmaneutral_e        = n_period * plasmaneutral_e* fact_flux / (GAMMA-1.d0)
plasmaneutral_i        = n_period * plasmaneutral_i* fact_flux / (GAMMA-1.d0)
#else
plasmaneutral        = n_period * plasmaneutral* fact_flux / (GAMMA-1.d0)
#endif
Prec                 = n_period * Prec         * fact_flux / (GAMMA-1.d0)
Prb                  = n_period * Prb          * fact_flux / (GAMMA-1.d0)
Prb_cooling          = n_period * Prb_cooling  * fact_flux / (GAMMA-1.d0)
aux_mom_par_int      = n_period * aux_mom_par_int * rho_norm / t_norm
aux_mom_par_ext      = n_period * aux_mom_par_ext * rho_norm / t_norm
aux_mom_par_tot      = n_period * aux_mom_par_tot * rho_norm / t_norm

#if (defined WITH_Neutrals) || (defined WITH_Impurities)
total_radiation             = n_period * total_radiation
total_radiation_cooling     = n_period * total_radiation_cooling
total_radiation_bg          = n_period * total_radiation_bg
total_radiation_phi         = n_period * total_radiation_phi
total_radiation_cooling_phi = n_period * total_radiation_cooling_phi
total_E_ion                 = n_period * total_E_ion
total_P_ei                  = n_period * total_P_ei
total_P_ion                 = n_period * total_P_ion
#endif

! --- Boundary integrals
vn_p0                =  n_period * vn_p0          * fact_flux 
qn_par               =  n_period * qn_par         * fact_flux  
qn_perp              =  n_period * qn_perp        * fact_flux 
kinpar_flux          =  n_period * kinpar_flux    * fact_flux  
viscopar_flux        =  n_period * viscopar_flux  * fact_flux
Dpar_part_flux       =  n_period * Dpar_part_flux * fact_part / t_norm2  
Dperp_part_flux      =  n_period * Dperp_part_flux* fact_part / t_norm2
vpar_part_flux       =  n_period * vpar_part_flux * fact_part / t_norm2
vperp_part_flux      =  n_period * vperp_part_flux* fact_part / t_norm2
neut_part_flux       =  n_period * neut_part_flux * fact_part / t_norm2
poynting_flux        =  n_period * poynting_flux  * fact_flux
if (L > 0) then
  int_B_norm         =  sqrt(n_period * int_B_norm / (2 * PI * L ))
else 
  write(*,*) 'WARNING: The length of contour : L = 0'
  int_B_norm         = 0.d0
endif

! --- Derived quantities
Volume       = Volume_in + Volume_ext
E_tot        = mag_tot + pressure     + kin_par_tot + kin_perp_tot 
E_in         = mag_in  + pressure_in  + kin_par_in  + kin_perp_in 
E_out        = mag_out + pressure_out + kin_par_out + kin_perp_out 
current_tot  = current_in + current_out
current_R_tot = current_R_in + current_R_out
heating_tot  = heating_in + heating_out
heating_impl_tot  = heating_impl_in + heating_impl_out
source_tot   = source_in  + source_out
Bgeo         = F0 / R_geo
current_MA   = current_in * 1.d-6 * (1.d0/fact_mu0) * (1/mu_zero)
current_R    = current_R_in * (1.d0/fact_mu0) * (1/mu_zero)
beta_p       = 4.d0 * pressure_in/(R_geo * current_in**2 )     * (GAMMA-1)*fact_mu0
beta_t       = 2.d0 * pressure_in / volume_in / Bgeo**2           * (GAMMA-1)/fact_mu0
beta_n       = 100.d0 * beta_t * Bgeo/current_MA * ES%LCFS_a
li3          = 2.d0 * mag_in /0.5  /( current_in**2 * R_geo ) * fact_mu0
li3_tot      = 2.d0 * mag_tot/0.5  /(current_tot**2 * R_geo ) * fact_mu0
sheath_heatflux =  gamma_stangeby * (gamma-1)/(2.d0*gamma) * vn_p0 ! the factor comes to obtain n T_e v from vn_p0
R_curr_cent  = sqrt(R2curr / current_tot) 
Z_curr_cent  = Z_curr_cent / current_tot 
vmec_beta_tot        = pressure/mag_pressure
vmec_beta_in         = pressure_in/mag_pressure_in
if (mag_pressure_out .ne. 0) then
  vmec_beta_out      = pressure_out/mag_pressure_out
else
  vmec_beta_out      = 0.d0
endif


! --- Externally calculated quantities
! --- Halo currents
#ifdef fullmhd   
  I_halo = 0.d0 ! Needs to be adapted for Full MHD
  TPF    = 0.d0 
#else
  call integrated_normal_bnd_curr(node_list, bnd_node_list, bnd_elm_list, I_halo, TPF, .false.)
#endif

! --- Safety factor at important locations
surface_list%n_psi = 4 
allocate( surface_list%psi_values(surface_list%n_psi) )
allocate( qval(surface_list%n_psi), radav(surface_list%n_psi) )
surface_list%psi_values(1) = ES%psi_axis + (ES%psi_bnd - ES%psi_axis) * 0.01d0
surface_list%psi_values(2) = ES%psi_axis + (ES%psi_bnd - ES%psi_axis) * 0.02d0
surface_list%psi_values(3) = ES%psi_axis + (ES%psi_bnd - ES%psi_axis) * 0.95d0 
surface_list%psi_values(4) = ES%psi_axis + (ES%psi_bnd - ES%psi_axis) * 0.99d0

call find_flux_surfaces(my_id,xpoint, xcase, node_list, element_list, surface_list)
call determine_q_profile(node_list, element_list, surface_list, ES%psi_axis, ES%psi_xpoint,    &
     ES%Z_xpoint, qval, radav)

q02 = qval(2)
q95 = qval(3)
q99 = qval(4) 


if (my_id .eq. 0) then 

  ! --- Get current time
  if (index_now > 0) then
    xt = xtime(index_now)
  else
    xt = 0.d0
  endif

  ! --- Export or save quantities
  loop_expr: do iexpr = 1, expr_list%n_expr
            
    select case ( trim(expr_list%expr(iexpr)%name) )

      case ( 'Time' )
        res(iexpr) = xt*t_norm2

      case ( 'index_now' )
        res(iexpr) = real(index_now) 

      case ( 'psi_axis' )
        res(iexpr) = ES%psi_axis 

      case ( 'R_axis' )
        res(iexpr) = ES%R_axis 

      case ( 'Z_axis' )
        res(iexpr) = ES%Z_axis

      case ( 'R_curr_cent' )
        res(iexpr) = R_curr_cent 

      case ( 'Z_curr_cent' )
        res(iexpr) = Z_curr_cent

      case ( 'psi_bnd' )
        res(iexpr) = ES%psi_bnd 

      case ( 'R_bnd' )
        res(iexpr) = ES%R_bnd 

      case ( 'Z_bnd' )
        res(iexpr) = ES%Z_bnd

      case ( 'E_tot' )
        res(iexpr) = E_tot 

      case ( 'E_in' )
        res(iexpr) = E_in 

      case ( 'E_out' )
        res(iexpr) = E_out

      case ( 'Wmag_tot' )
        res(iexpr) = mag_tot 

      case ( 'Wmag_in' )
        res(iexpr) = mag_in 

      case ( 'Wmag_out' )
        res(iexpr) = mag_out 
     
      case ( 'Thermal_tot' )
        res(iexpr) = pressure 

      case ( 'Thermal_in' )
        res(iexpr) = pressure_in 

      case ( 'Thermal_out' )
        res(iexpr) = pressure_out 

      case ( 'Thermal_e_tot' )
        res(iexpr) = pressure_e

      case ( 'Thermal_e_in' )
        res(iexpr) = pressure_e_in 

      case ( 'Thermal_e_out' )
        res(iexpr) = pressure_e_out 

      case ( 'Thermal_i_tot' )
        res(iexpr) = pressure_i

      case ( 'Thermal_i_in' )
        res(iexpr) = pressure_i_in 

      case ( 'Thermal_i_out' )
        res(iexpr) = pressure_i_out 

      case ( 'Kin_par_tot' )
        res(iexpr) = kin_par_tot 

      case ( 'Kin_par_in' )
        res(iexpr) = kin_par_in 

      case ( 'Kin_par_out' )
        res(iexpr) = kin_par_out 

      case ( 'Kin_perp_tot' )
        res(iexpr) = kin_perp_tot 

      case ( 'Kin_perp_in' )
        res(iexpr) = kin_perp_in 

      case ( 'Kin_perp_out' )
        res(iexpr) = kin_perp_out 

      case ( 'Part_tot' ) 
        res(iexpr) = density_tot 

      case ( 'Part_in' ) 
        res(iexpr) = density_in 

      case ( 'Part_out' ) 
        res(iexpr) = density_out 

      case ( 'NPart_tot' ) 
        res(iexpr) = neut_particles_tot  

      case ( 'Helicity_tot' )
        res(iexpr) = helicity_tot 

      case ( 'Mag_work_tot' )
        res(iexpr) = mag_work_tot

      case ( 'Thm_work_tot' )
        res(iexpr) = thermal_work_tot

      case ( 'Part_src_tot' )
        res(iexpr) = source_tot

      case ( 'Part_src_in' )
        res(iexpr) = source_in

      case ( 'Part_src_out' )
        res(iexpr) = source_out

      case ( 'Heat_src_tot' )
        res(iexpr) = heating_tot

      case ( 'Heat_src_in' )
        res(iexpr) = heating_in

      case ( 'Heat_src_out' )
        res(iexpr) = heating_out

      case ( 'Viscpar_diss' )
        res(iexpr) = viscopar_dissip_tot

      case ( 'Visc_diss' )
        res(iexpr) = visco_dissip_tot

      case ( 'Fric_diss' )
        res(iexpr) = friction_dissip_tot

      case ( 'Wmag_src_tot' )
        res(iexpr) = mag_source_tot 

      case ( 'Ohmic_tot' )
        res(iexpr) = ohm_tot 

      case ( 'Ohmic_in' )
        res(iexpr) = ohm_in 

      case ( 'Ohmic_out' )
        res(iexpr) = ohm_out 

      case ( 'saw_ene' )
        res(iexpr) = saw_energy_tot

#if (defined WITH_Neutrals) || (defined WITH_Impurities)
      case ( 'Rad_tot' )
        res(iexpr) = total_radiation

      case ( 'Rad_bg_tot' )
        res(iexpr) = total_radiation_bg
#endif

      case ( 'P_vn' )
        res(iexpr) = vn_p0 

      case ( 'qn_par' )
        res(iexpr) = qn_par 

      case ( 'qn_perp' )
        res(iexpr) = qn_perp

      case ( 'sheath_heat' )
        res(iexpr) = sheath_heatflux 

      case ( 'kinpar_flux' )
        res(iexpr) = kinpar_flux

      case ( 'Poynting_flx' )
        res(iexpr) = poynting_flux

      case ( 'vispar_flux' )
        res(iexpr) = viscopar_flux

      case ( 'Dpar_pt_flx' )
        res(iexpr) = Dpar_part_flux

      case ( 'Dperp_pt_flx' )
        res(iexpr) = Dperp_part_flux

      case ( 'vpar_pt_flx' )
        res(iexpr) = vpar_part_flux

      case ( 'vperp_pt_flx' )
        res(iexpr) = vperp_part_flux

      case ( 'neut_pt_flx' )
        res(iexpr) = neut_part_flux

      case ( 'Ip_tot' )
        res(iexpr) = current_tot 

      case ( 'Ip_in' )
        res(iexpr) = current_in 

      case ( 'Ip_out' )
        res(iexpr) = current_out 

      case ( 'int3d_jR_tot' )
        res(iexpr) = current_R_tot 

      case ( 'int3d_jR_in' )
        res(iexpr) = current_R_in 

      case ( 'int3d_jR_out' )
        res(iexpr) = current_R_out 

      case ( 'li3' )
        res(iexpr) = li3

      case ( 'li3_tot' )
        res(iexpr) = li3_tot

      case ( 'beta_p' )
        res(iexpr) = beta_p 

      case ( 'beta_t' )
        res(iexpr) = beta_t 

      case ( 'beta_n' )
        res(iexpr) = beta_n 

      case ( 'area' )
        res(iexpr) = area 

      case ( 'volume' )
        res(iexpr) = volume_in

      case ( 'q02' )
        res(iexpr) = q02 

      case ( 'q95' )
        res(iexpr) = q95 

      case ( 'q99' )
        res(iexpr) = q99 

      case ( 'I_halo' )
        res(iexpr) = I_halo 

      case ( 'TPF_halo' )
        res(iexpr) = TPF 

      case ('int_dBn_norm')
        res(iexpr) = int_B_norm

      case ( 'Px' )
        res(iexpr) = Px
      
      case ( 'Py' )
        res(iexpr) = Py

      case ( 'LCFS_Rgeo' )
        res(iexpr) = ES%LCFS_Rgeo 

      case ( 'LCFS_a' )
        res(iexpr) = ES%LCFS_a

      case ( 'LCFS_epsilon' )
        res(iexpr) = ES%LCFS_epsilon

      case ( 'LCFS_kappa' )
        res(iexpr) = ES%LCFS_kappa

      case ( 'LCFS_deltaU' )
        res(iexpr) = ES%LCFS_deltaU 

      case ( 'LCFS_deltaL' )
        res(iexpr) = ES%LCFS_deltaL

#if (defined WITH_Neutrals) || (defined WITH_Impurities)
      case ( 'tot_radiated' )
        res(iexpr) = total_radiation
#else
      case ( 'tot_radiated' )
        res(iexpr) = 0.d0 
#endif

    end select
            
  end do loop_expr

  ! ---- Print out some data 
  write(*,'(A,3e14.6,A)') ' Time : ',xt,xt*t_norm,t_norm, ' [s]'
  if (use_pellet) then 
    write(*,'(A,4e14.6)')   ' Integrals_3D, PELLET            : ',pellet_volume, total_pellet_volume, total_pellet_particles, total_plasma_particles
  endif
#if (defined WITH_Neutrals) || (defined WITH_Impurities)
  if (using_spi) then
    write(*,'(A)')   ' Integrals_3D, SPI               : '
    do i = 1, n_spi_tot

       n_spi_tmp = 0
       do i_inj = 1,n_inj
         n_spi_tmp = n_spi_tmp + n_spi(i_inj)                                                   
         if (i <= n_spi_tmp)  exit !< Determine the injection location index of the fragment
       end do

       if (pellets(i)%spi_radius > 0. .and. pellets(i)%spi_abl > 0.) then
          write(*,'(A,i14)')    "Pellet number                = ", i
          write(*,'(A,3f14.6)') "Pellet coordinates (R,Z,phi) = ", pellets(i)%spi_R, pellets(i)%spi_Z, pellets(i)%spi_phi
          write(*,'(A,3f14.6)') "Pellet velocity    (R,Z,phi) = ", pellets(i)%spi_Vel_R, pellets(i)%spi_Vel_Z, &
               pellets(i)%spi_Vel_RxZ
          write(*,'(A,3es14.6)')"Pellet ablation (radius,abl) = ", pellets(i)%spi_radius, pellets(i)%spi_abl
          write(*,'(A,f14.6)')  "Pellet species               = ", pellets(i)%spi_species

          ns_radius_tmp   = pellets(i)%spi_radius * ns_radius_ratio

          if (ns_radius_tmp < ns_radius_min) then
             ns_radius_tmp = ns_radius_min
          end if

          if (ns_delta_minor_rad .gt. 0.) then
             ! i.e., with poloidally elongated ablation cloud
             ! in this case the analytical formula below is approximate (usually it agrees with the numerical integral within a few percents)
             V_ns  = PI * pellets(i)%spi_R * ns_tor_norm * ns_radius_tmp * min(ns_delta_minor_rad,ns_radius_tmp)
             if (drift_distance(i_inj) /= 0.d0) then
               V_ns_drift  = PI * (pellets(i)%spi_R + drift_distance(i_inj)) * ns_tor_norm * ns_radius_tmp * min(ns_delta_minor_rad,ns_radius_tmp)
             end if
          else
             ! i.e., standard case with circular ablation cloud in the poloidal plane
             ! in this case the ablation source volume is given by the exact analytical formula as derived by E. Nardon
             V_ns  = PI * pellets(i)%spi_R * ns_tor_norm * ns_radius_tmp**2.d0
             if (drift_distance(i_inj) /= 0.d0) then
               V_ns_drift  = PI * (pellets(i)%spi_R + drift_distance(i_inj)) * ns_tor_norm * ns_radius_tmp**2.d0
             end if
          endif
          
          write(*,'(A,2es14.6,f14.6)') "Source vol (num,an,diff %)   = ", pellets(i)%spi_vol, V_ns, 1d2*(pellets(i)%spi_vol - V_ns)/V_ns
          if (abs((pellets(i)%spi_vol - V_ns)/V_ns) .gt. 0.1d0) write(*,*) "WARNING: Difference larger than 10% "

          if (drift_distance(i_inj) /= 0.d0) then 
            write(*,'(A,2es14.6,f14.6)') "Drifted source vol (num,an,diff %)   = ", pellets(i)%spi_vol_drift, V_ns_drift, 1d2*(pellets(i)%spi_vol_drift - V_ns_drift)/V_ns_drift
            if (abs((pellets(i)%spi_vol_drift - V_ns_drift)/V_ns_drift) .gt. 0.1d0) write(*,*) "WARNING: Difference larger than 10% "
          end if

          ! recommended ablation source radius in the poloidal direction from ns_radius / (R*ns_deltaphi) = B_pol/B_tor
          write(*,'(A,2f14.6)') "Source pol rad (actual,recom)= ", ns_radius_tmp, pellets(i)%spi_R * ns_deltaphi * pellets(i)%spi_grad_psi / abs(F0)
          if (drift_distance(i_inj) /= 0.d0) then 
            write(*,'(A,2f14.6)') "Drifted source pol rad (actual,recom)= ", ns_radius_tmp, (pellets(i)%spi_R + drift_distance(i_inj)) * ns_deltaphi * pellets(i)%spi_grad_psi_drift / abs(F0)
          end if
       end if
    end do
  endif
#endif
  write(*,'(A,2es14.6,A)') ' Volume_in                       : ',xt,Volume_in,' [m^3]'
  write(*,'(A,2es14.6,A)') ' Volume_ext                      : ',xt,Volume_ext,' [m^3]'
  write(*,'(A,2es14.6,A)') ' Volume                          : ',xt,volume,' [m^3]'
  write(*,'(A,2es14.6,A)') ' Surface area                    : ',xt,surface_area, '[m^2]'
  write(*,'(A,4es14.6,A)') ' density  (total/in/out)         : ',xt,density_tot,  density_in,  density_out,'[ 10^20/m^3]'
  write(*,'(A,4es14.6,A)') ' pressure (total/in/out)         : ',xt,pressure/1.d6, pressure_in/1.d6, pressure_out/1.d6,' [MJ]'
  write(*,'(A,4es14.6,A)') ' magnetic pressure (total/in/out): ',xt,mag_pressure/1.d6, mag_pressure_in/1.d6, mag_pressure_out/1.d6, ' [MJ]'
  write(*,'(A,4es14.6,A)') ' kinetic parallel (total/in/out) : ',xt,kin_par_tot/1.d6, kin_par_in/1.d6, kin_par_out/1.d6,' [MJ]'
  write(*,'(A,4es14.6,A)') ' kinetic perp (total/in/out)     : ',xt,kin_perp_tot/1.d6, kin_perp_in/1.d6, kin_perp_out/1.d6,' [MJ]'
  write(*,'(A,4e14.6,A)')  ' parallel momentum (total/in/out): ',xt,mom_par_tot, mom_par_int, mom_par_ext,' [kg m/s]'
  write(*,'(A,4es14.6,A)') ' magnetic (total/in/out)         : ',xt,mag_tot/1.d6, mag_in/1.d6, mag_out/1.d6,' [MJ]'
  write(*,'(A,3es14.6,A)') ' current  (in/out)               : ',xt,current_in/1.d6, current_out/1.d6, ' [MA]'
  write(*,'(A,3es14.6,A)') ' int J*R  (in/out)               : ',xt,current_R_in, current_R_out, ' [Am]'
  write(*,'(A,3es14.6,A)') ' heating  (in/out)               : ',xt,heating_in/1d6, heating_out/1.d6 ,' [MW]'
  write(*,'(A,4es14.6,A)') ' Implicit heating  (total/in/out): ',xt,heating_impl_tot/1.d6,heating_impl_in/1.d6, heating_impl_out/1.d6 ,' [MW]'
  write(*,'(A,3es14.6,A)') ' source   (in/out)               : ',xt,source_in, source_out,' [10^20/m^3/s]'
  write(*,'(A,4es14.6,A)') ' Ohmic    (in/out)               : ',xt,Ohm_tot/1.d6, Ohm_in/1.d6, Ohm_out/1.d6,' [MW]'

  write(*,'(A,2es14.6)')   ' li(3)                           : ',xt, li3 
  write(*,'(A,2es14.6)')   ' betap(1)                        : ',xt, beta_p
  write(*,'(A,4es14.6)')   ' beta (total/in/out)             : ',xt,beta_tot,beta_in,beta_out
  write(*,'(A,4es14.6)')   ' vmec_beta (total/in/out)        : ',xt,vmec_beta_tot,vmec_beta_in,vmec_beta_out
  ! beta_tot/in/out is calculated via beta = 2 mu0 <p/B^2> and differs from vmec_beta_tot/in/out which is calculated via beta = 2 mu0 <p> / <B^2>
  ! Also note that VMEC and other codes do not use the factor (GAMMA-1) in the definition of pressure.

  write(*,'(A)')           ' sum ,time ,density_tot, pressure, Wkin_par, Wkin_perp, Wmag, Ohm, heating, source'

  write(*,'(A,20es14.6)')  ' sum ',xt,density_tot,pressure/1.d6,kin_par_tot/1.d6,kin_perp_tot/1.d6,mag_tot/1.d6, &
                                 Ohm_tot/1.d6,heating_in/1d6+heating_out/1.d6 ,source_in+source_out


  if (use_ncs .or. use_ics) then
    write(*,'(A)') '----------------------------------------'
    write(*,'(A)') ' Kinetic neutral integrals on fluid side                  '
    write(*,'(A,4es14.6,A)') ' Ion source (aux_rho0), Recomb loss                : ',xt,xt*t_norm, Nion, Nrec,' [#/m^3/s]'
    write(*,'(A,5es14.6,A)') ' Parallel momentum source(aux_mom_par0) (total/in/out): ',xt,xt*t_norm,aux_mom_par_tot, aux_mom_par_int, aux_mom_par_ext,' [kg m/s]'
#ifdef WITH_TiTe
    write(*,'(A,3es14.6,A)') ' Heat source electrons (aux_E0_Te)         : ',xt,xt*t_norm, plasmaneutral_e/1.d6, ' [MW]'
    write(*,'(A,3es14.6,A)') ' Heat source ions (aux_E0_Ti)         : ',xt,xt*t_norm, plasmaneutral_i/1.d6, ' [MW]'
#else
    write(*,'(A,3es14.6,A)') ' Heat source (aux_E0)         : ',xt,xt*t_norm, plasmaneutral/1.d6, ' [MW]'
#endif
    write(*,'(A,5es14.6,A)') ' Prec, Prb, Prb_cooling       : ',xt,xt*t_norm,Prec/1.d6,Prb/1.d6,Prb_cooling/1.d6,' [MW]'
    write(*,'(A)') '----------------------------------------'
  endif !use_ncs .or. use_ics 

#if (defined WITH_Neutrals) || (defined WITH_Impurities)
  write(*,'(A,4es14.6)')   ' Integrals_3D, MGI              : ', total_n_particles_inj, total_n_particles
  write(*,'(A,1e14.6,A)')  ' Radiation power (incl. backgr. imp) : ', total_radiation/1.d6, ' [MW]'
  write(*,'(A,1e14.6,A)')  ' Radiation power BACKGROUND     : ', total_radiation_bg/1.d6, ' [MW]'
  write(*,'(A,1e14.6,A)')  ' Radiation power SANITY         : ', sum(total_radiation_phi)/1.d6, ' [MW]'
  write(*,'(A,1e14.6,A)')  ' Radiation cooling power        : ', total_radiation_cooling/1.d6, ' [MW]'
  write(*,'(A,1e14.6,A)')  ' Radiation cooling power SANITY : ', sum(total_radiation_cooling_phi)/1.d6, ' [MW]'
  

  if (with_neutrals) then
    write(*,'(A,1e14.6,A)') ' Ionization power              : ', total_P_ion/1.d6, ' [MW]'
  else if (with_impurities) then ! With CE assumption, it's easier to obtain the total ionization energy then get the ionization power by finite difference
    write(*,'(A,1e14.6,A)') ' Ionization energy              : ', total_E_ion/1.d6, ' [MJ]'
  endif
  if (with_TiTe)  write(*,'(A,1e14.6,A)') ' Electron-ion energy exchange    : ', total_P_ei/1.d6, ' [MW]'

  if (index_now > 1) then
    xtime_radiation(index_now) = xtime_radiation(index_now-1) + t_norm * tstep * total_radiation
  else if (index_now == 1) then
    xtime_radiation(index_now) = t_norm * tstep * total_radiation
  end if
  if (index_now > 0) then
  xtime_rad_power(index_now) = total_radiation
  xtime_rad_cooling_power(index_now) = total_radiation_cooling
  end if

  if (output_prad_phi) then
    open(20,file="total_radiation_phi.dat",action="write",position="append")
    do i_phi = 1, n_plane
      write(20,'(1e14.6)') total_radiation_phi(i_phi)/1.d6
    end do
    close (20)
  end if

  if (with_neutrals) then
    if (index_now > 1) then
      xtime_E_ion(index_now) = xtime_E_ion(index_now-1) + t_norm * tstep * total_P_ion
    else if (index_now == 1) then
      xtime_E_ion(index_now) = t_norm * tstep * total_P_ion
    end if
    if (index_now > 0) then
      xtime_E_ion_power(index_now) = total_P_ion
    end if
  else if (with_impurities) then ! For CE assumption, we directly give the total ionization energy
    if (index_now > 0) then
      xtime_E_ion(index_now) = total_E_ion
    end if
  endif
  if (with_TiTe) then
    if (index_now > 0) xtime_P_ei(index_now) = total_P_ei
  endif

#endif

  do k = 1, n_var
    write(*,'(A,i3,A20,2es14.6)') ' min/max', k, trim(variable_names(k)), V_min(k), V_max(k)
  end do


  if (index_now > 0 ) then

    E_tot_t(index_now)               = E_tot 
    Wmag_tot_t(index_now)            = mag_tot 
    Ohmic_tot_t(index_now)           = ohm_tot
    visco_dissip_tot_t(index_now)    = visco_dissip_tot
    viscopar_dissip_tot_t(index_now) = viscopar_dissip_tot
    friction_dissip_tot_t(index_now) = friction_dissip_tot
    thmwork_tot_t(index_now)         = thermal_work_tot 
    magwork_tot_t(index_now)         = mag_work_tot 
    Thermal_tot_t(index_now)         = pressure 
    Thermal_e_tot_t(index_now)       = pressure_e
    Thermal_i_tot_t(index_now)       = pressure_i
    Helicity_tot_t(index_now)        = helicity_tot
    Ip_tot_t(index_now)              = current_tot 
    current_t(index_now)             = current_in 
    Kin_par_tot_t(index_now)         = kin_par_tot
    Kin_perp_tot_t(index_now)        = kin_perp_tot 
    flux_Pvn_t(index_now)            = vn_p0
    flux_qpar_t(index_now)           = qn_par 
    flux_qperp_t(index_now)          = qn_perp 
    flux_kinpar_t(index_now)         = kinpar_flux 
    flux_poynting_t(index_now)       = poynting_flux 
    li3_t(index_now)                 = li3
    li3_tot_t(index_now)             = li3_tot
    viscopar_flux_t(index_now)       = viscopar_flux
    heat_src_tot_t(index_now)        = heating_tot
    heat_src_in_t(index_now)         = heating_in 
    heat_src_out_t(index_now)        = heating_out           
    part_src_tot_t(index_now)        = source_tot
    part_src_in_t(index_now)         = source_in
    part_src_out_t(index_now)        = source_out
    area_t(index_now)                = area
    volume_t(index_now)              = volume_in
    mag_ener_src_tot(index_now)      = mag_source_tot
    beta_n_t(index_now)              = beta_n
    beta_t_t(index_now)              = beta_t
    beta_p_t(index_now)              = beta_p
    npart_tot_t(index_now)           = neut_particles_tot 
    density_tot_t(index_now)         = density_tot
    density_in_t(index_now)          = density_in
    density_out_t(index_now)         = density_out
    pressure_in_t(index_now)         = pressure_in
    pressure_out_t(index_now)        = pressure_out     
    part_flux_Dpar_t(index_now)      = Dpar_part_flux
    part_flux_Dperp_t(index_now)     = Dperp_part_flux
    part_flux_vpar_t(index_now)      = vpar_part_flux
    part_flux_vperp_t(index_now)     = vperp_part_flux
    npart_flux_t(index_now)          = neut_part_flux 
    Px_t(index_now)                  = Px
    Py_t(index_now)                  = Py
 
    !--- Calculate time derivatives at previous step (second order accuracy)
    if (index_now > 2) then
      dt_back   = xtime(index_now-1) - xtime(index_now - 2)
      dt_now    = xtime(index_now)   - xtime(index_now - 1)
      r_dt2     = (dt_now/dt_back)**2.d0

      dE_tot_dt(index_now-1) = (E_tot_t(index_now) - r_dt2*E_tot_t(index_now-2) &
        -(1.d0-r_dt2)*E_tot_t(index_now-1))  / (dt_now + dt_back*r_dt2) / t_norm

      dWmag_tot_dt(index_now-1) = (Wmag_tot_t(index_now) - r_dt2*Wmag_tot_t(index_now-2) &
        -(1.d0-r_dt2)*Wmag_tot_t(index_now-1))  / (dt_now + dt_back*r_dt2) / t_norm

      dthermal_tot_dt(index_now-1) = (thermal_tot_t(index_now) - r_dt2*thermal_tot_t(index_now-2) &
        -(1.d0-r_dt2)*thermal_tot_t(index_now-1))  / (dt_now + dt_back*r_dt2) / t_norm

      dkinperp_tot_dt(index_now-1) = (kin_perp_tot_t(index_now) - r_dt2*kin_perp_tot_t(index_now-2) &
        -(1.d0-r_dt2)*kin_perp_tot_t(index_now-1))  / (dt_now + dt_back*r_dt2) / t_norm

      dkinpar_tot_dt(index_now-1) = (kin_par_tot_t(index_now) - r_dt2*kin_par_tot_t(index_now-2) &
        -(1.d0-r_dt2)*kin_par_tot_t(index_now-1))  / (dt_now + dt_back*r_dt2) / t_norm

      dpart_tot_dt(index_now-1) = (density_tot_t(index_now) - r_dt2*density_tot_t(index_now-2) &
        -(1.d0-r_dt2)*density_tot_t(index_now-1))  / (dt_now + dt_back*r_dt2) / t_norm

      dnpart_tot_dt(index_now-1) = (npart_tot_t(index_now) - r_dt2*npart_tot_t(index_now-2) &
        -(1.d0-r_dt2)*npart_tot_t(index_now-1))  / (dt_now + dt_back*r_dt2) / t_norm
      
      dPx_dt(index_now-1) = (Px_t(index_now) - r_dt2*Px_t(index_now-2) &
        -(1.d0-r_dt2)*Px_t(index_now-1))  / (dt_now + dt_back*r_dt2) / t_norm
      
      dPy_dt(index_now-1) = (Py_t(index_now) - r_dt2*Py_t(index_now-2) &
        -(1.d0-r_dt2)*Py_t(index_now-1))  / (dt_now + dt_back*r_dt2) / t_norm

      if (with_impurities) then ! Calculate the ionization energy change
        xtime_E_ion_power(index_now-1) = (xtime_E_ion(index_now) - r_dt2*xtime_E_ion(index_now-2) &
        -(1.d0-r_dt2)*xtime_E_ion(index_now-1))  / (dt_now + dt_back*r_dt2) / t_norm
      endif

    endif

    !--- Estimate time derivatives for 1st tstep (1st order accuracy)
    if (index_now == 2) then
      dt_now    = xtime(index_now) - xtime(index_now - 1)

      dE_tot_dt(index_now-1)       = (E_tot_t(index_now)-E_tot_t(index_now-1))               / dt_now / t_norm

      dWmag_tot_dt(index_now-1)    = (Wmag_tot_t(index_now)-Wmag_tot_t(index_now-1))         / dt_now / t_norm

      dthermal_tot_dt(index_now-1) = (thermal_tot_t(index_now)-thermal_tot_t(index_now-1))   / dt_now / t_norm

      dkinperp_tot_dt(index_now-1) = (kin_perp_tot_t(index_now)-kin_perp_tot_t(index_now-1)) / dt_now / t_norm

      dkinpar_tot_dt(index_now-1)  = (kin_par_tot_t(index_now) - kin_par_tot_t(index_now-1)) / dt_now / t_norm

      dpart_tot_dt(index_now-1)    = (density_tot_t(index_now)-density_tot_t(index_now-1))   / dt_now / t_norm

      dnpart_tot_dt(index_now-1)   = (npart_tot_t(index_now)-npart_tot_t(index_now-1))       / dt_now / t_norm
      
      dPx_dt(index_now-1)       = (Px_t(index_now)-Px_t(index_now-1))                        / dt_now / t_norm
      
      dPy_dt(index_now-1)       = (Py_t(index_now)-Py_t(index_now-1))                        / dt_now / t_norm

      if (with_impurities) then ! Calculate the ionization energy change
        xtime_E_ion_power(index_now-1) = (xtime_E_ion(index_now)-xtime_E_ion(index_now-1))   / dt_now / t_norm
      endif

    endif


  endif

endif !--- my_id
end subroutine int3d_new 

#ifndef NOMPIVERSION
end module mod_integrals3D
#endif
