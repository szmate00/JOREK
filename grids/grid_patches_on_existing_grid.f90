subroutine grid_patches_on_existing_grid(node_list, element_list)
!-----------------------------------------------------------------------
! subroutine defines a flux surface aligned finite element grid
! inclduing a single x-point
!-----------------------------------------------------------------------

use constants
use tr_module 
use data_structure
use grid_xpoint_data
use mod_export_restart
use mod_eqdsk_tools
use mod_element_rtree

! --- Input parameters
use phys_module, only:     n_wall_blocks, xpoint, n_flux, n_tht, freeboundary

implicit none

! --- Routine parameters
type (type_node_list),    intent(inout) :: node_list
type (type_element_list), intent(inout) :: element_list

! --- local variables
type (type_surface_list) :: flux_list, sep_list

type (type_node_list),    pointer :: node_list_tmp,    node_list_tmp2,    node_list_new
type (type_element_list), pointer :: element_list_tmp, element_list_tmp2, element_list_new

integer             :: my_id, i_ext
integer             :: n_grids(12)
integer             :: n_seg_prev
real*8              :: seg_prev(n_seg_max)
integer             :: n_loop, i, j, k, index, ier
logical             :: include_axis, include_xpoint, include_psi
logical             :: normal_eqdsk, normal_eqdsk_wall
logical             :: freeb_save
logical, parameter  :: plot_grid = .true.
character*2         :: char_patch
character*256       :: filename

my_id  = 1 ! Just don't want the printout...

write(*,*) ' '
write(*,*) ' '
write(*,*) '*************************************'
write(*,*) '*************************************'
write(*,*) '*************************************'
write(*,*) '* Adding extension patches to grid  *'
write(*,*) '*************************************'
write(*,*) '*************************************'
write(*,*) '*************************************'
write(*,*) ' '
write(*,*) ' '


!-------------------------------------------------------------------------------------------!
!---------------------------- Initialise some internal data --------------------------------!
!-------------------------------------------------------------------------------------------!


n_grids(1) = n_flux
n_grids(2) = n_tht
if (n_flux .eq. 0) then
! A pre-existing non-X-point grid does not use n_tht to describe its
! central ring.  Recover the actual number from the grid instead.  In
! particular, a polar grid is built with n_pol, which may differ from
! the otherwise unrelated input value n_tht.
  n_grids(2) = 0
  do i=1,node_list%n_nodes
    if (node_list%node(i)%axis_node) n_grids(2) = n_grids(2) + 1
  enddo
endif

freeb_save = freeboundary    ! Disable freeboundary for this routine
freeboundary = .false.       ! This allows to export restart files for diagnosing the grid patches



!-------------------------------- Redefine neighbours
if (sum(element_list%element(1)%neighbours) .eq. 0) then
  call update_neighbours_basic(element_list,node_list)
endif
call temporary_element_sizes(node_list, element_list)
call export_restart(node_list, element_list, 'grid_no_patch')

! --- Allocate data structures for new nodes and initialize them
allocate(node_list_tmp,node_list_tmp2,node_list_new)
call init_node_list(node_list_tmp, n_nodes_max, 0, n_var)
call init_node_list(node_list_tmp2, n_nodes_max, 0, n_var)
call init_node_list(node_list_new, n_nodes_max, 0, n_var)
call tr_register_mem(sizeof(node_list_tmp),"node_list_tmp")
call tr_register_mem(sizeof(node_list_tmp2),"node_list_tmp2")
call tr_register_mem(sizeof(node_list_new),"node_list_new")
! --- Allocate data structures for new elements and initialize them
allocate(element_list_tmp,element_list_tmp2,element_list_new)
call tr_register_mem(sizeof(element_list_tmp),"element_list_tmp")
call tr_register_mem(sizeof(element_list_tmp2),"element_list_tmp2")
call tr_register_mem(sizeof(element_list_new),"element_list_new")
! --- Copy structure into work structure
call copy_node_structure(node_list_new, element_list_new, node_list, element_list)

!-------------------------------- Now the wall extension
do i_ext = 1,n_wall_blocks
  call define_extension_patch(node_list_new, element_list_new, node_list_tmp, element_list_tmp, n_seg_prev, seg_prev, i_ext)
  call update_neighbours_basic(element_list_tmp,node_list_tmp)
  call update_boundary_types  (element_list_tmp,node_list_tmp, 0)
  ! --- create restart file for vtk plots BEG
  if (i_ext .lt. 10) write(char_patch,'(i1)') i_ext
  if (i_ext .ge. 10) write(char_patch,'(i2)') i_ext
  write(filename,'(A10,A)')'grid_patch',trim(char_patch)
  call temporary_element_sizes(node_list_tmp, element_list_tmp)
  call export_restart(node_list_tmp, element_list_tmp, filename)
  ! --- create restart file for vtk plots END
  call join_grid_patches(node_list_new,  element_list_new, &
                         node_list_tmp,  element_list_tmp, &
                         node_list_tmp2, element_list_tmp2, 0)
  call copy_node_structure(node_list_new, element_list_new, node_list_tmp2, element_list_tmp2)
  call update_neighbours_basic(element_list_new,node_list_new)
  call update_boundary_types  (element_list_new,node_list_new, 0)
enddo



! --- Finalise grid (element size, nodes index etc.)
include_axis   = .true.
include_xpoint = .true.
include_psi    = .true.
if (n_flux .eq. 0) include_axis = (n_grids(2) .gt. 0)
if (n_flux .eq. 0) include_xpoint = .false.
call get_eqdsk_style(normal_eqdsk, normal_eqdsk_wall, ier)
if ( (ier .ne. 0) .and. (n_flux .eq. 0) ) include_psi = .false.
call finish_grid(node_list, element_list, node_list_new, element_list_new, n_grids, include_axis, include_xpoint, include_psi)

call export_restart(node_list, element_list, 'jorek_grid')




!----------------------------------- Print a python file that plots a cross with the 4 nodes of each element
if (plot_grid) then
  n_loop = element_list%n_elements
  open(101,file='plot_elements.py')
    write(101,'(A)')         '#!/usr/bin/env python'
    write(101,'(A)')         'import numpy as N'
    write(101,'(A)')         'import pylab'
    write(101,'(A)')         'def main():'
    write(101,'(A,i6,A)')    ' r = N.zeros(',4*n_loop,')'
    write(101,'(A,i6,A)')    ' z = N.zeros(',4*n_loop,')'
    do j=1,n_loop
      do i=1,2
        index = element_list%element(j)%vertex(i)
        write(101,'(A,i6,A,f15.4)') ' r[',4*(j-1)+2*i-2,'] = ',node_list%node(index)%x(1,1,1)
        write(101,'(A,i6,A,f15.4)') ' z[',4*(j-1)+2*i-2,'] = ',node_list%node(index)%x(1,1,2)
        index = element_list%element(j)%vertex(i+2)
        write(101,'(A,i6,A,f15.4)') ' r[',4*(j-1)+2*i-1,'] = ',node_list%node(index)%x(1,1,1)
        write(101,'(A,i6,A,f15.4)') ' z[',4*(j-1)+2*i-1,'] = ',node_list%node(index)%x(1,1,2)
      enddo
    enddo
    write(101,'(A,i6,A)')    ' for i in range (0,',n_loop,'):'
    write(101,'(A)')         '  pylab.plot(r[4*i:4*i+2],z[4*i:4*i+2], "r")'
    write(101,'(A)')         '  pylab.plot(r[4*i+2:4*i+4],z[4*i+2:4*i+4], "g")'
    do j=1,n_loop
      do i=1,4
        index = element_list%element(j)%vertex(i)
        write(101,'(A,i6,A,f15.4)') ' r[',4*(j-1)+i-1,'] = ',node_list%node(index)%x(1,1,1)
        write(101,'(A,i6,A,f15.4)') ' z[',4*(j-1)+i-1,'] = ',node_list%node(index)%x(1,1,2)
      enddo
    enddo
    write(101,'(A,i6,A)')    ' for i in range (0,',n_loop,'):'
    write(101,'(A)')         '  pylab.plot(r[4*i:4*i+4],z[4*i:4*i+4], "b")'
    write(101,'(A)')         ' pylab.axis("equal")'
    write(101,'(A)')         ' pylab.show()'
    write(101,'(A)')         ' '
    write(101,'(A)')         'main()'
  close(101)
endif


freeboundary  = freeb_save   ! Reset freeboundary to input value

! --- Deallocate data structures for new nodes and initialize them
deallocate(node_list_tmp,node_list_tmp2,node_list_new)
call tr_unregister_mem(sizeof(node_list_tmp),"node_list_tmp")
call tr_unregister_mem(sizeof(node_list_tmp2),"node_list_tmp2")
call tr_unregister_mem(sizeof(node_list_new),"node_list_new")

! --- Deallocate data structures for new elements and initialize them
deallocate(element_list_tmp,element_list_tmp2,element_list_new)
call tr_register_mem(sizeof(element_list_tmp),"element_list_tmp")
call tr_register_mem(sizeof(element_list_tmp2),"element_list_tmp2")
call tr_register_mem(sizeof(element_list_new),"element_list_new")



return
end subroutine grid_patches_on_existing_grid







